grammar net.sourceforge.ufoai.UFOScript with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate ufoScript "http://ufoai.org/UFOScript"

// starting point for parsing a ufo script file
UFOScript:
	(blocks+=NamedBlock)*;

NamedBlock:
	type=ID name=ID "{"
		(=>properties+=Property)*
	"}";

Property:
	type=ID => value=PropertyValue;

PropertyValue:
	PropertyValueString
	| PropertyValueBoolean
	| PropertyValueNumber
	| PropertyValueBlock
	| PropertyValueID;

PropertyValueString:
	value=STRING;

PropertyValueBoolean:
	value=UFO_BOOLEAN;

PropertyValueNumber:
	value=Double;

PropertyValueID:
	value=[NamedBlock];

PropertyValueBlock:
	"{" {PropertyValueBlock} (=>properties+=Property)* "}";

/* ==================== terminals ================================ */

Double:
	(INT | REAL);

/* ==================== terminals ================================ */

terminal REAL:
	('0'..'9')* '.' ('0'..'9')+;

terminal UFO_BOOLEAN:
	'true' | 'false';
