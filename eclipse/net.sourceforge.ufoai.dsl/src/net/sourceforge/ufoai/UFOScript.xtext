grammar net.sourceforge.ufoai.UFOScript with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate ufoScript "http://ufoai.org/UFOScript"

// starting point for parsing a ufo script file
UFOScript:
	(blocks+=NamedBlock)*;

NamedBlock:
	type=ID name=ID "{" =>
		(=>properties+=Property)*
	=> "}";

Property:
	type=ID => value=PropertyValue;

PropertyValue:
	=> PropertyValueBlock
	| PropertyValueBoolean
	| PropertyValueNull
	| PropertyValueNumber
	| PropertyValueNamedConst
	| PropertyValueID
	| PropertyValueString;

PropertyValueString:
	value=STRING;

PropertyValueBoolean:
	value=UFO_BOOLEAN;

PropertyValueNumber:
	value=Double;

PropertyValueID:
	value=[NamedBlock];

PropertyValueNamedConst:
	value=NAMED_CONST;

PropertyValueNull:
	value=NULL;

PropertyValueBlock:
	"{" {PropertyValueBlock} (=>properties+=Property)* => "}";

/* ==================== terminals ================================ */

Double:
	(INT | REAL);

/* ==================== terminals ================================ */

terminal NAMED_CONST:
	('A'..'Z') ('A'..'Z' | '0'..'9' | '_')*;

terminal REAL:
	('-'|'+')?('0'..'9')* '.' ('0'..'9')+;

terminal INT returns ecore::EInt:
	('-'|'+')?('0'..'9')+;

terminal UFO_BOOLEAN:
	'true' | 'false';

terminal NULL:
	'null';

terminal ID:
	('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'-'|'/'|'0'..'9')*;
