grammar net.sourceforge.ufoai.UFOScript with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate ufoScript "http://ufoai.org/UFOScript"

// starting point for parsing a ufo script file
UFOScript:
	(blocks+=TopLevelNode*);

TopLevelNode:
	UITopLevelNode | Aircraft | Tech | Building | Item | Equipment | Sprite | Particle | Tips | Chrtemplate | Team |
	Terrain | Sequence | MapDef;

/* ==================== UI TOP LEVEL NODES ================================ */
UITopLevelNode:
	UINodeComponent | UINodeWindow | UIFont;

/** a window can only extend another window */
UINodeWindow:
	"window" name=UINodeID ("extends" superWindow=[UINodeWindow])? "{"
	/* optional block with window properties that might be empty */
	("{" (properties+=UIPropertyDefinition)* "}")?
	/* nodes */
	(nodes+=UINode)*
	"}";

/** components can extend any ui node */
UINodeComponent:
	"component" name=UINodeID "extends" ((superComponent=[UINodeComponent]) | (type=UINodeType)) "{"
	("{" (properties+=UIPropertyDefinition)* "}")?
	(properties+=UIPropertyDefinition)*
	(nodes+=UINode)*
	"}";

/* ==================== NODES ================================ */
UINodeType:
	"panel" | "image" | "button" | "model" | "text" | "string" | "confunc" | "func" | "textentry" | "baseinventory" |
	"baselayout" | "bar" | "basemaps" | "checkbox" | "battlescape" | "controls" | "data" | "cvarlistener" | "container" |
	"ekg" | "editor" | "item" | "geoscape" | "linechart" | "keybinding" | "messagelist" | "material_editor" |
	"optionlist" | "option" | "radar" | "optiontree" | "rows" | "radiobutton" | "sequence" | "selectbox" | "tab" |
	"spinner" | "text2" | "tbar" | "texture" | "textlist" | "todo" | "timer" | "video" | "zone" | "vscrollbar";

UINode:
	UINodePanel | UINodeImage | UINodeButton | UINodeModel | UINodeText | UINodeString | UINodeConfunc | UINodeFunc |
	UINodeTextEntry | UINodeBaseinventory | UINodeBar | UINodeBasemap | UINodeBaselayout | UINodeCheckbox |
	UINodeBattlescape | UINodeControls | UINodeContainer | UINodeData | UINodeCvarlistener | UINodeEkg | UINodeEditor |
	UINodeItem | UINodeGeoscape | UINodeLinechart | UINodeKeybinding | UINodeMessagelist | UINodeMaterial_Editor |
	UINodeOptionlist | UINodeOption | UINodeRadar | UINodeOptiontree | UINodeRows | UINodeRadiobutton | UINodeSequence |
	UINodeSelectbox | UINodeTab | UINodeSpinner | UINodeText2 | UINodeTbar | UINodeTexture | UINodeTextlist | UINodeTodo |
	UINodeTimer | UINodeVideo | UINodeZone | UINodeVscrollbar | UIComponentUse;

UINodePanel:
	"panel" name=UINodeID "{" ("{" (properties+=UIPropertyDefinition)* "}")? (nodes+=UINode)* "}";

UIComponentUse:
	component=[UINodeComponent] name=UINodeID "{" ("{" (properties+=UIPropertyDefinition)* "}" (nodes+=UINode)* | (properties+=UIPropertyDefinition)*) "}";

UINodeImage:
	"image" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeButton:
	"button" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeModel:
	"model" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeText:
	"text" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeString:
	"string" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeConfunc:
	"confunc" name=UINodeID "{" (statements+=UIFuncStatements)* "}";

UINodeFunc:
	"func" name=UINodeID "{" (statements+=UIFuncStatements)* "}";

UINodeTextEntry:
	"textentry" name=UINodeID "{" (statements+=UIPropertyDefinition)* "}";

UINodeBar:
	"bar" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeBaseinventory:
	"baseinventory" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeBaselayout:
	"baselayout" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeBasemap:
	"basemap" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeBattlescape:
	"battlescape" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeCheckbox:
	"checkbox" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeContainer:
	"container" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeControls:
	"controls" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeCvarlistener:
	"cvarlistener" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeData:
	"data" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeEditor:
	"editor" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeEkg:
	"ekg" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeGeoscape:
	"geoscape" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeItem:
	"item" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeKeybinding:
	"keybinding" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeLinechart:
	"linechart" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeMaterial_Editor:
	"material_editor" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeMessagelist:
	"messagelist" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeOption:
	"option" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeOptionlist:
	"optionlist" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeOptiontree:
	"optiontree" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeRadar:
	"radar" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeRadiobutton:
	"radiobutton" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeRows:
	"rows" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeSelectbox:
	"selectbox" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeSequence:
	"sequence" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeSpinner:
	"spinner" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeTab:
	"tab" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeTbar:
	"tbar" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeText2:
	"text2" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeTextlist:
	"textlist" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeTexture:
	"texture" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeTimer:
	"timer" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeTodo:
	"todo" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeVideo:
	"video" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeVscrollbar:
	"vscrollbar" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UINodeZone:
	"zone" name=UINodeID "{" (properties+=UIPropertyDefinition)* "}";

UIFont:
	"font" name=ID "{" => "}";

/* ==================== NODE PROPERTIES ================================ */

/**
 * TODO hide this object from the scope, i check creating UFO_ID
 * from fragment STRING_ but it everytime generate many warnings
 */
UIPropertyDefinition:
	name=("clickOutAbort" | "height" | "lineselected" | "background" | "bindingwidth" | "bordercolor" | "bottomIcon" | "flipicon" | "color1" | "collapsed" | "disabled" | "pos" | "displayammo" | "align" | "wheelscrollable" | "number" | "color2" | "max" | "lines" | "hidewhenunused" | "columns" | "onAbort" | "count" | "noticepos" | "integer" | "visiblewhen" | "mousefx" | "onrclick" | "repeat" | "layout" | "anim" | "baseid" | "longlines" | "scale" | "skin" | "preventtypingescape" | "color" | "string" | "indexed" | "onWindowOpened" | "displayweapon" | "viewsize" | "tabwidth" | "texl" | "onclick" | "width" | "rows" | "onwheelup" | "font" | "fullsize" | "onScriptLoaded" | "onmouseleave" | "layoutColumns" | "icon" | "clipoverflow" | "iconUnchecked" | "lastselectedid" | "starlayout" | "cvar" | "topIcon" | "onmouseenter" | "dataid" | "ghost" | "shiftincreasefactor" | "invis" | "onClick" | "onwheel" | "nohover" | "displayammoofweapon" | "selectcolor" | "displayunavailableammoofweapon" | "onEvent" | "onselect" | "fill" | "tag" | "onviewchange" | "onchange" | "isPassword" | "dropdown" | "displayavailableontop" | "top" | "clickdelay" | "containerlike" | "image" | "iconChecked" | "closebutton" | "displayaxes" | "axescolor" | "contentalign" | "direction" | "current" | "left" | "scrollspeed" | "border" | "displayunavailableitem" | "size" | "preventratio" | "origin" | "modal" | "excluderect" | "delta" | "textselected" | "num" | "filter" | "lineheight" | "onChange" | "autoscale" | "texh" | "padding-right" | "src" | "bgcolor" | "onwheeldown" | "value" | "rotatewithmouse" | "viewpos" | "onmclick" | "iconIndeterminate" | "omega" | "readonly" | "layoutMargin" | "min" | "padding" | "onWindowClosed" | "lastdiff" | "nosound" | "dragbutton" | "keyspace" | "angles" | "stringValue" | "tooltip" | "label" | "onEnd" | "timeout") value=UIPropertyValue;

UIPropertyValue:
	UIPropertyValueString
	| UIPropertyValueBoolean
	| UIPropertyValueNumber
	| UIPropertyValueFunction;

UIPropertyValueString:
	value=STRING;

UIPropertyValueBoolean:
	value=UFO_BOOLEAN;

UIPropertyValueNumber:
	value=INT;

UIPropertyValueFunction:
	"{" {UIPropertyValueFunction} (statements+=UIFuncStatements)* "}";

/* ==================== GENERAL UI STUFF ================================ */
UIFuncStatements:
	UICvarAssignment | UINodePropertyAssignment | UIFuncIfStatement | UIFuncDeleteCvar | UICommand | UIFuncCall;

UIFuncCall:
	"call" UINodePath;

UICommand:
	"cmd" name=STRING;

UIFuncDeleteCvar:
	"delete" name=UICvar;

UICvarAssignment:
	name=UICvar "=" value=UIValue;

UINodePropertyAssignment:
	UINodePath "@" property=UINodeProperty "=" value=UIValue;

UIFuncIfStatement:
	"if" "(" conditions+=UIConditionalExpressions+ ")" "{" statements+=UIFuncStatements* "}" ("elif" "("
	conditionsElif+=UIConditionalExpressions+ ")" "{" statements+=UIFuncStatements* "}")* ("else" "{"
	statements+=UIFuncStatements* "}")?;

UIConditionalExpressions:
	conditionalorexpression=UIConditionalOrExpression;

UIConditionalOrExpression:
	conditionalandexpression+=UIConditionalAndExpression ('||' conditionalandexpression+=UIConditionalAndExpression)*;

UIConditionalAndExpression:
	equalityexpression+=UIEqualityExpression ('&&' equalityexpression+=UIEqualityExpression)*;

UIEqualityExpression:
	expression+=UIConditionalExpression (('==' | '!=') expression+=UIConditionalExpression)*;

UIConditionalExpression:
	{UIConditionalExpression} (name=UIValue comparator=UIComparator value=UIValue);

UIValue:
	STRING | INT | UIFuncParameter | UICvar | UFO_BOOLEAN;

UIImage:
	STRING | UIFuncParameter | UICvar;

UIFuncParameter:
	"<" INT ">";

UIComparator:
	">" | "<" | "eq" | "ne" | "!=" | "==" | "<=" | ">=";

UINodePath:
	"*node:" ((("this" ("." "parent")?) | "root") ".")? name=UINodeID (=> '.' UINodeID)*;

UICvar:
	"*cvar:" ID;

UINodeID:
	'background' | UINodeType | ID;

	// TODO: add the property keywords
UINodeProperty:
	'invis' | ID;

/* ==================== non-ui stuff============================== */
Tech:
	"tech" name=ID "{"
	"provides" provides=[Item]
	"}";

Sprite:
	"sprite" name=ID "{"
	"image" image=STRING
	"texl" texl=STRING
	"size" size=STRING
	"pack64" pack64=UFO_BOOLEAN
	"}";

Team:
	"team" name=ID "{"
	"size" size=STRING
	"hit_particle" hitParticle=[Particle]
	"armour" armour=UFO_BOOLEAN
	"tech" tech=[Tech]
	"race" race=STRING
	"name" teamName=STRING
	"death_texture" deathTexture=STRING
	"weapons" weapons=UFO_BOOLEAN
	"onlyWeapon" onlyWeapon=[Item]
	names=TeamNames
	models=TeamModels
	sounds=TeamSounds
	templates=TeamTemplates
	resistance=TeamResistance
	"}";

TeamSounds:
	"sounds" "{"
	(sounds+=TeamGender+)
	"}";

TeamModels:
	"models" "{"
	(models+=TeamGender+)
	"}";

TeamNames:
	"names" "{"
	(names+=TeamGenderNames+)
	"}";

TeamGenderNames:
	TeamGender | TeamLastNames;

TeamLastNames:
	"lastname" "{" => "}";

TeamTemplates:
	"templates" "{"
	(templates+=[Chrtemplate]+)
	"}";

TeamResistance:
	"resistance" "{" => "}";

TeamGender:
	TeamFemale | TeamMale | TeamNeutral;

TeamFemale:
	"female" "{" => "}";

TeamMale:
	"male" "{" => "}";

TeamNeutral:
	"neutral" "{" => "}";

Terrain:
	"terrain" name=ID "{" => "}";

Chrtemplate:
	"chrtemplate" name=ID "{"
	"rate" rate=STRING
	"strength" strength=STRING
	"speed" speed=STRING
	"accuracy" accuracy=STRING
	"mind" mind=STRING
	"close" close=STRING
	"heavy" heavy=STRING
	"assault" assault=STRING
	"sniper" sniper=STRING
	"explosive" explosive=STRING
	"health" health=STRING "}";

Particle:
	"particle" name=ID "{"
	(stages+=ParticleStages*)
	"}";

ParticleStages:
	ParticleInitStage | ParticleRunStage | ParticleThinkStage | ParticlePhysicsStage;

ParticlePhysicsStage:
	"physics" "{" => "}";

ParticleThinkStage:
	"think" "{" => "}";

ParticleRunStage:
	"run" "{" => "}";

ParticleInitStage:
	"init" "{" => "}";

MapDef:
	"mapdef" name=ID "{" => "}";

Tips:
	"tips" name=ID "{" => "}";

Sequence:
	"sequence" name=ID "{" => "}";

Item:
	"item" name=ID "{" => "}";

Equipment:
	"equipment" name=ID "{"
	"name" equipName=STRING
	(items+=[Item] STRING)+
	"}";

Building:
	"building" name=ID "{"
	"name" buildingName=STRING
	"size" size=STRING
	"image" image=STRING
	"type" type=BuildingType
	"depends" depends=[Building]
	"pedia" pedia=[Tech]
	"fixcosts" fixcosts=STRING
	"build_time" buildTime=STRING
	"varcosts" varCosts=STRING
	"map_name" mapName=STRING
	"capacity" capacity=STRING
	"ondisable" onDisable=STRING
	"onenable" onEnable=STRING
	"ondestroy" onDestroy=STRING
	"onconstruct" onConstruct=STRING
	"mandatory" mandatory=UFO_BOOLEAN
	"max_count" maxCount=STRING
	"starting_employees" startingEmployees=STRING
	"level" level=STRING "}";

BuildingType:
	"lab" | "hospital" | "aliencont" | "storage" | "hangar" | "smallhangar" | "quarters" | "workshop" | "power" |
	"command" | "amstorage" | "entrance" | "missile" | "laser" | "radar";

Aircraft:
	"aircraft" name=ID "{"
	params=AircraftParam (slots+=AircraftSlot)*
	"building" building=[Building]
	"name" aircraftName=STRING
	"defaultname" defaultName=STRING
	"type" type=AircraftType
	"model" model=STRING
	"price" price=STRING
	"numteam" numTeam=STRING
	"size" size=STRING
	"image" image=STRING
	"}";

AircraftType:
	"ufo" | "interceptor" | "transporter";

AircraftSlot:
	"slot" "{"
	"type" type=STRING
	"position" position=STRING
	"size" size=STRING "
	}";

AircraftParam:
	"param" "{"
	"speed" speed=STRING
	"maxspeed" maxspeed=STRING
	"shield" shield=STRING
	"ecm" ecm=STRING
	"damage" damage=STRING
	"accuracy" accuracy=STRING
	"range" range=STRING
	"antimatter" antimatter=STRING
	"}";

/* ==================== terminals ================================ */
terminal UFO_BOOLEAN:
	'true' | 'false';
