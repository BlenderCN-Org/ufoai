// ==================
// SKIRMISH MENU
// ==================

window skirmish
{
	// ==================
	// background
	// ==================

	pic background
	{
		image		"ui/skirmish_bg"
	}

	cmpMenuButton button_back
	{
		string		"_Back"
		pos			"90 200"
		onClick		{ cmd "mn_pop;" }
	}

	// ====================
	// start game button
	// ====================
	cmpMenuButton button_start
	{
		string		"_Start game"
		pos			"90 80"
		onClick		{ cmd "sk_start;" }
	}

	string settings
	{
		string		"_Skirmish game settings"
		pos			"500 80"
		size		"450 30"
		font		"f_big"
	}

	tab create_natigation {
		{
			pos	"462 150"
			size	"500 40"
			cvar	*cvar:mn_skirmish_config_tab
			onChange {
				call *node:root.sk_update_config_navigation
			}
		}
		option map {
			label	"_Map"
			value	"1"
		}
		option option {
			label	"_Options"
			value	"2"
		}
	}

	panel panel_option {
		{
			pos		"495 210"
			size	"475 370"
		}

		// ===================
		// EQUIPMENT AI
		// ===================
		string equipment_alien
		{
			string		"_Equipment AI"
			pos			"10 0"
			size		"450 20"
		}

		textentry slot_alien_equipment
		{
			string		*cvar:ai_equipment
			image		"ui/button_green_verysmall"
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"35 30"
			size		"256 34"
			disabled	true
			onWheelUp	{ cmd "sk_prevequip ai_equipment;" }
			onWheelDown	{ cmd "sk_nextequip ai_equipment;" }
		}
		custombutton prev_alien_equipment
		{
			pos			"0 30"
			size		"35 35"
			icon		prev
			onWheelUp	{ cmd "sk_prevequip ai_equipment;" }
			onWheelDown	{ cmd "sk_nextequip ai_equipment;" }
			onClick		{ cmd "sk_prevequip ai_equipment;" }
		}
		custombutton next_alien_equipment
		{
			pos			"285 30"
			size		"35 35"
			icon		next
			onWheelUp	{ cmd "sk_prevequip ai_equipment;" }
			onWheelDown	{ cmd "sk_nextequip ai_equipment;" }
			onClick		{ cmd "sk_nextequip ai_equipment;" }
		}

		// ===================
		// EQUIPMENT
		// ===================
		string equipment
		{
			string		"_Equipment"
			pos			"10 75"
			size		"450 20"
		}

		textentry slot_equipment
		{
			string		*cvar:cl_equip
			image		"ui/button_green_verysmall"
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"35 105"
			size		"256 34"
			disabled	true
			onWheelUp	{ cmd "sk_prevequip cl_equip;" }
			onWheelDown	{ cmd "sk_nextequip cl_equip;" }
		}
		custombutton prev_equipment
		{
			pos			"0 105"
			size		"35 35"
			icon		prev
			onWheelUp	{ cmd "sk_prevequip cl_equip;" }
			onWheelDown	{ cmd "sk_nextequip cl_equip;" }
			onClick		{ cmd "sk_prevequip cl_equip;" }
		}
		custombutton next_equipment
		{
			pos			"285 105"
			size		"35 35"
			icon		next
			onWheelUp	{ cmd "sk_prevequip cl_equip;" }
			onWheelDown	{ cmd "sk_nextequip cl_equip;" }
			onClick		{ cmd "sk_nextequip cl_equip;" }
		}

		// ===================
		// AI_NUMACTORS
		// ===================
		string ai_numactors_str
		{
			string		"_Max. AI actors:"
			pos			"10 275"
			size		"200 20"
			tooltip		"_Max AI actors for this game"
		}
		string ai_numactors_value
		{
			string		*cvar:ai_numaliens
			pos			"310 275"
			size		"50 20"
			textalign	ALIGN_CC
		}
		spinner ai_numactors_change
		{
			current		*cvar:ai_numaliens
			image		"ui/spinner_blue"
			pos			"368 274"
			min			1
			max			30
			delta		1
		}

		// =====================
		// MAXSOLDIER PER PLAYER
		// =====================
		string maxsoldierperplayer_str
		{
			string		"_Max. soldiers:"
			tooltip		"_Sets the amount of soldiers"
			pos			"10 250"
			size		"300 20"
		}
		string maxsoldierperplayer_value
		{
			string		*cvar:sv_maxsoldiersperplayer
			pos			"310 250"
			size		"50 20"
			textalign	ALIGN_UC
		}
		spinner maxsoldierperplayer_change
		{
			current		*cvar:sv_maxsoldiersperplayer
			image		"ui/spinner_blue"
			pos			"368 249"
			min			1
			max			8
			delta		1
		}

		// ===================
		// TEAM
		// ===================
		string team_str
		{
			string		"_Team:"
			pos			"10 300"
			size		"200 20"
			tooltip		"_Select which side you want to play with"
		}

		selectbox team_box
		{
			image	"ui/selectbox_green"
			pos		"230 300"
			size	"200 20"
			color	"0.6 0.6 0.6 0.3"
			cvar	*cvar:cl_teamdef
			dataid	OPTION_TEAMDEFS
		}
	}

	cmpMapPanel panel_map {
		{
			pos		"495 210"
			size	"475 370"
		}

		// ===================
		// UFOS
		// ===================

		string ufos_str
		{
			string		"_UFO:"
			pos			"10 245"
			size		"200 20"
			tooltip		"_Select the ufo that should be used"
		}

		selectbox ufos_box
		{
			image	"ui/selectbox_green"
			pos		"240 245"
			size	"200 20"
			color	"0.6 0.6 0.6 0.3"
			cvar	*cvar:rm_ufo
			dataid	OPTION_UFOS
		}

		// ===================
		// DROPSHIPS
		// ===================

		string dropships_str
		{
			string		"_Dropship:"
			pos			"10 290"
			size		"200 20"
			tooltip		"_Select the dropship that should be used"
		}

		selectbox dropships_box
		{
			image	"ui/selectbox_green"
			pos		"240 290"
			size	"200 20"
			color	"0.6 0.6 0.6 0.3"
			cvar	*cvar:rm_drop
			dataid	OPTION_DROPSHIPS
		}
	}

	// ===================
	// Interaction
	// ===================

	/**
	 * @param<1> boolean to hide or show
	 */
	confunc skirmish_hide_dropships {
		*node:root.panel_map.dropships_str@invis = <1>
		*node:root.panel_map.dropships_box@invis = <1>
	}

	/**
	 * @param<1> boolean to hide or show
	 */
	confunc skirmish_hide_ufos {
		*node:root.panel_map.ufos_str@invis = <1>
		*node:root.panel_map.ufos_box@invis = <1>
	}

	func sk_update_config_navigation {
		*panel_map@invis = true
		*panel_option@invis = true
		if ( *cvar:mn_skirmish_config_tab == 1 ) {
			*panel_map@invis = false
		}
		elif ( *cvar:mn_skirmish_config_tab == 2 ) {
			*panel_option@invis = false
		}
	}

	// ===================
	// INIT
	// ===================
	func onInit
	{
		// TODO: Allow changing the difficulty via menu
		cmd	"game_setmode;set ai_numaliens 8;set ai_equipment multiplayer_alien;set difficulty 0;"
		cmd "set mn_serverday 1;mn_getmaps;define cl_teamdef phalanx;"
		*cvar:mn_skirmish_config_tab = 1
		call *node:root.sk_update_config_navigation
	}
}
