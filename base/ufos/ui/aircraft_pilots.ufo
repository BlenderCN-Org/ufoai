// ==================
// AIRCRAFT PILOTS MENU
// ==================

window aircraft_pilots extends iaircraft
{

	data selected {
	}

	BaseContainerPanelV2 base_container
	{
		panel mainBody
		{
			{}
			// Title bar
			panel header
			{
				{}
				string title
				{
					string			"_Pilots"
				}
			}
			// ==================
			// pilot list
			// ==================

			panel actors_background2 {
				{
				pos			"5 225"
				size		"354 310"
				border		"2"
				bordercolor	"0.527 0.6 0.21 1"
				}
				image icon_soldier {
					src	"icons/head_pilot"
					pos		"5 15"
					size	"42 42"
				}
				panel soldierlist {
					pos				"55 08"
					size			"270 298"
					padding			6
					layout			LAYOUT_TOP_DOWN_FLOW
					layoutMargin	2
					wheelscrollable	true

					onViewChange {
						*node:parent.soldierlist_scroll@fullsize = <fullsize>
						*node:parent.soldierlist_scroll@current = <viewpos>
						*node:parent.soldierlist_scroll@viewsize = <viewsize>
					}
				}
				vscrollbar soldierlist_scroll {
					image			"ui/scrollbar_v_green"
					pos				"325 5"
					height			"298"
					hidewhenunused	true
					current			0
					viewsize		8
					fullsize		50

					onChange {
						*node:parent.soldierlist@viewpos = <current>
					}
				}
			}
		}

		// ==================
		// aircraft
		// ==================
		model aircraft
		{
			src			*cvar:mn_aircraft_model_display
			pos			"276 568"
			size		"395 191"
			origin		"0 -10 10"
			angles		"180 10 -120"
			autoscale	true
		}

		model assigned_pilot_body
		{
			{
				src		*cvar:mn_pilot_body
				skin	*cvar:mn_pilot_body_skin
				anim	"stand_still"
				angles	"0 90 90"
				scale	"5.5 5.5 5.5"
				pos		"20 581"
				size	"62 62"
				origin	"0 70 0"
			}

			model assigned_pilot_head
			{
				src		*cvar:mn_pilot_head
				skin	*cvar:mn_pilot_head_skin
				tag		"tag_head"
			}
		}

		string assigned_pilot_name
		{
			string		*cvar:mn_pilot_name
			pos			"95 581"
			size		"200 20"
			contentalign	ALIGN_UL
		}
	}

	// ==================
	// additional stuff
	// ==================

	/**
	 * @note iaircraft interface
	 */
	confunc aircraft_change	{
		cmd "ui_team_fill pilot;"
		call *node:root.aircraft_checkbuttons
		call *node:root.base_container.mainBody.aircraft_info.aircraft_healthbar.setValues(<cvar:mn_aircraft_health>)
		if ( *node:root.selected@integer >= 0 ) {
			cmd "aircraft_soldierlist_select <node:root.selected@integer>;"
		}
		*node:root.base_container.mainBody.aircraftlist.aircraft_list@lineselected = <1>
		// absent from base, go back to the main GUI
		if ( *cvar:mn_aircraftinbase == 0 ) {
			cmd "ui_pop;ui_push aircraft_pilots;"
		}
	}

	func onWindowOpened
	{
		*node:root.selected@integer = -1
		call *node:root.reset_character_cvars
		cmd "mn_select_aircraft <cvar:mn_aircraft_id>;"
	}

	panel pilot_info {
		{
			pos			"730 100"
			size		"285 665"
		}
		skillspilot pilotskills
		{
			pos			"10 120"
		}

		healthbar actorhealth
		{
			pos			"10 240"
		}
		actor_info local
		{
			{
				pos		"01 320"
			}
			text name
			{
				string		*cvar:mn_name
				pos			"10 320"
				size		"290 20"
				color		"0.56 0.81 0.76 1"
				ghost       true
			}
		}
	}
}
