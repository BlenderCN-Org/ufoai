// ====================
// MATERIAL EDITOR MENU
// ====================

window material_editor {
	{
		pos "50 50"
		size "600 650"
		dragbutton	true
		closebutton true
		image		ui/popup_alpha_tile
		string		"_Material editor"
	}
	
	material_editor editor {
		pos "30 55"
		size "210 210"
		padding 4
		bgcolor	"0.5 0.5 0.5 0.3"
		selectcolor "1 1 1 1"

		onViewChange {
			*(abstractscrollbar)editor_scroll current <viewpos>
			*(abstractscrollbar)editor_scroll viewsize <viewsize>
			*(abstractscrollbar)editor_scroll fullsize <fullsize>
		}
		onChange {
			*cvar:me_image_id <num>
		}
	}

	vscrollbar editor_scroll {
		image		ui/scrollbar_v_green
		pos			"250 55"
		height		210
		hidewhenunused true
		onChange	{
			*editor viewpos <current>
		}
	}

	string imagename {
		string		"*cvar me_imagename"
		pos			"340 55"
		size		"250 20"
	}

	/** 
	 * @param<1> boolean to hide or show the panel 
	 * @param<2> value for bump 
	 * @param<3> value for hardness 
	 * @param<4> value for parallax
	 * @param<5> value for specular
	 */
	confunc hideshaders {
		*shader_attributes invis <1>
		*cvar:me_bump <2>
		*cvar:me_hardness <3>
		*cvar:me_parallax <4>
		*cvar:me_specular <5>
	}

	panel shader_attributes {
		{
			pos		"30 280"
			size	"200 150"
		}
		string hardness_str
		{
			string		"_Hardness:"
			pos			"0 0"
			size		"80 20"
			font		"f_verysmall"
		}
		pic hardness_rahmen
		{
			image		ui/bar_rahmen
			pos			"90 5"
			size		"108 16"
		}
		bar hardness_bar
		{
			current		"*cvar me_hardness"
			pos			"94 10"
			size		"100 5"
			color		"0 .8 0 1"
			max			3.0
			onChange	{ cmd "mn_materialeditor_changevalue <cvar:me_image_id> -1 hardness <current>;" }
		}

		string bump_str
		{
			string		"_Bump:"
			pos			"0 20"
			size		"80 20"
			font		"f_verysmall"
		}
		pic bump_rahmen
		{
			image		ui/bar_rahmen
			pos			"90 25"
			size		"108 16"
		}
		bar bump_bar
		{
			current		"*cvar me_bump"
			pos			"94 30"
			size		"100 5"
			color		"0 .8 0 1"
			max			3.0
			onChange	{ cmd "mn_materialeditor_changevalue <cvar:me_image_id> -1 bump <current>;" }
		}

		string specular_str
		{
			string		"_Specular:"
			pos			"0 40"
			size		"80 20"
			font		"f_verysmall"
		}
		pic specular_rahmen
		{
			image		ui/bar_rahmen
			pos			"90 45"
			size		"108 16"
		}
		bar specular_bar
		{
			current		"*cvar me_specular"
			pos			"94 50"
			size		"100 5"
			color		"0 .8 0 1"
			max			3.0
			onChange	{ cmd "mn_materialeditor_changevalue <cvar:me_image_id> -1 specular <current>;" }
		}

		string parallax_str
		{
			string		"_Parallax:"
			pos			"0 60"
			size		"80 20"
			font		"f_verysmall"
		}
		pic parallax_rahmen
		{
			image		ui/bar_rahmen
			pos			"90 65"
			size		"108 16"
		}
		bar parallax_bar
		{
			current		"*cvar me_parallax"
			pos			"94 70"
			size		"100 5"
			color		"0 .8 0 1"
			max			3.0
			onChange	{ cmd "mn_materialeditor_changevalue <cvar:me_image_id> -1 parallax <current>;" }
		}
	}

	/** 
	 * @param<1> boolean to hide or show the stages panel 
	 */
	confunc hidestages {
		*stage_attributes invis <1>
	}

	confunc updatestagevalues {
		*cvar:rotate_hz <1>
		*cvar:rotate_deg <2>
	}
	
	panel stages {
		{
			pos		"280 110"
			size	"180 130"
		}
		text stages {
			dataid		TEXT_MATERIAL_STAGES
			pos			"0 0"
			size		"160 90"
			bgcolor		"0.5 0.5 0.5 0.3"
			selectcolor	"1 1 1 1"
			font		"f_verysmall"
			lineheight	15
			rows		6
			onClick		{
				*cvar:me_stage_id <lineselected>
				cmd "mn_materialeditor_selectstage <cvar:me_image_id> <lineselected>;" 
			}
			onViewChange {
				*(abstractscrollbar)stages.stage_scroll current <viewpos>
				*(abstractscrollbar)stages.stage_scroll viewsize <viewsize>
				*(abstractscrollbar)stages.stage_scroll fullsize <fullsize>
				*(abstractscrollbar)stages.stage_scroll height <height>
			}
		}

		vscrollbar stage_scroll {
			image		ui/scrollbar_v_green
			pos			"165 0"
			height		90
			hidewhenunused true
			onChange	{
				*stages.stages viewpos <current>
			}
		}
	
		button stage_new {
			string		"_+"
			pos			"0 100"
			size		"40 28"
			image		ui/button_green_verysmall
			font		f_verysmall
			color		"0 0.5 0 1"
			selectcolor	"1 1 1 1"
			onClick	{ cmd "mn_materialeditor_newstage <cvar:me_image_id>;" }
		}
	
		button stage_remove {
			string		"_-"
			pos			"50 100"
			size		"40 28"
			image		ui/button_green_verysmall
			font		f_verysmall
			color		"0 0.5 0 1"
			selectcolor	"1 1 1 1"
			onClick	{ cmd "mn_materialeditor_removestage <cvar:me_image_id> <cvar:me_stage_id>;" }
		}
	}

	panel stage_attributes {
		{
			pos "280 270"
			size "280 100"
		}
		textentry rotate_hz
		{
			string		"*cvar me_rotate_hz"
			image		ui/button_green_verysmall
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	cl
			padding		6
			font		"f_verysmall"
			pos			"0 0"
			size		"60 30"
			onChange	{
				cmd "mn_materialeditor_changevalue <cvar:me_image_id> <cvar:me_stage_id> rotate.hz <cvar:me_rotate_hz>;" 
			}
		}

		textentry rotate_deg
		{
			string		"*cvar me_rotate_deg"
			image		ui/button_green_verysmall
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	cl
			padding		6
			font		"f_verysmall"
			pos			"64 0"
			size		"60 30"
			onChange	{
				cmd "mn_materialeditor_changevalue <cvar:me_image_id> <cvar:me_stage_id> rotate.deg <cvar:me_rotate_deg>;" 
			}
		}
	}
	
	func onInit {
		// general cvars
		*cvar:me_image_id -1
		*cvar:me_stage_id -1
		*cvar:me_imagename ""
		// shader cvars
		*cvar:me_bump 0
		*cvar:me_hardness 0
		*cvar:me_parallax 0
		*cvar:me_specular 0
		// stage cvars
		*cvar:me_rotate_hz 0
		*cvar:me_rotate_deg 0

		cmd "hideshaders true 0 0 0 0;"
		cmd "hidestages true;"
		cmd "mn_materialeditor;"
	}
	
	func onClose {
		// general cvars
		cmd "del me_image_id; del me_stage_id; del me_imagename;"
		// shader cvars
		cmd "del me_bump; del me_hardness; del me_parallax; del me_specular;"
		// stage cvars
		cmd "del me_rotate_hz; del me_rotate_deg"
	}
}
