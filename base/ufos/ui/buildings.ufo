// ==============
// BUILDINGS
// ==============

window buildings
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image back_ul
	{
		src		"ui/base_ul"
		pos			"0 0"
	}

	image back_ur
	{
		src		"ui/base_ur"
		pos			"512 0"
	}

	image back_lr
	{
		src		"ui/base_lr"
		pos			"512 256"
	}

	basemap thebasemap
	{
		pos			"0 96"
		size		"721 672"
	}

	cmpBaseHeader baseHeader {
	}

	textlist buildings
	{
		dataid		TEXT_BUILDINGS
		pos			"767 120"
		size		"209 570"
		lineheight	30
		color		"0 .78 0 1"
		selectcolor	"1 1 1 1"
		rows		"19"
		longlines	LONGLINES_PRETTYCHOP
		textalign	ALIGN_CC
		onClick		{
			cmd "buildings_click <lineselected>;"
			if (((*cvar:mn_building_image eq "base/aliencont") || (*cvar:mn_building_image eq "base/drop")) || (*cvar:mn_building_image eq "base/workshop")) {
				*node:popup_buildingpreview@width = 855
				*node:popup_buildingpreview.building_pic@width = 512
			} else {
				*node:popup_buildingpreview@width = 599
				*node:popup_buildingpreview.building_pic@width = 256
			}
		}
		onViewChange {
			*node:root.buildings_scroll@fullsize = <fullsize>
			*node:root.buildings_scroll@current = <viewpos>
		}
		onWheel {
			*node:root.buildings_scroll@current = <viewpos>
		}
	}

	vscrollbar buildings_scroll {
		image		"ui/scrollbar_v_green"
		pos			"976 120"
		height		"570"
		current		0
		viewsize	19
		fullsize	19
		hidewhenunused	true
		onChange	{
			*node:root.buildings@viewpos = <current>
		}
	}

	checkbox building_preview
	{
		tooltip		"_Open/close Building preview window"
		image		"ui/checkbox_green"
		pos			"770 700"
		size		"20 20"
		current		1
		onChange	{
			if ( *node:root.building_preview@current == 0 ) {
				cmd "ui_close popup_buildingpreview;"
			} elif (((*cvar:mn_building_image eq "base/aliencont") || (*cvar:mn_building_image eq "base/drop")) || (*cvar:mn_building_image eq "base/workshop")) {
				*node:popup_buildingpreview@width = 855
				*node:popup_buildingpreview.building_pic@width = 512
				 cmd "ui_push child popup_buildingpreview buildings;"
			} else {
				*node:popup_buildingpreview@width = 599
				*node:popup_buildingpreview.building_pic@width = 256
				 cmd "ui_push child popup_buildingpreview buildings;"
			}
		}
	}

	string txt_preview
	{
		string		"_Building preview"
		pos			"800 700"
		size		"180 20"
		onClick		{
			call *node:root.building_preview@toggle
		}
	}

	confunc mn_buildings_reset
	{
		*node:root.buildings@lineselected	= -1
		*cvar:mn_building_name	= "_Select building"
		*cvar:mn_building_image	= ""
		*cvar:mn_building_info	= ""
		*cvar:mn_building_status = ""
		cmd "ui_resetdata TEXT_BUILDING_INFO;"
	}

	func onWindowOpened
	{
		cmd "building_init;"
		call *node:root.mn_buildings_reset
		cmd "ui_push_child popup_buildingpreview buildings;"
	}

	func onWindowClosed
	{
		cmd "reset_building_current 0;"
	}
}

// =============================
// POPUP WINDOW BUILDINGPREVIEW
//==============================

window popup_buildingpreview
{
	{
		pos			"15 330"
		size		"599 345"
		dragbutton	true
		closebutton	true
		image		"ui/popup_alpha_tile"
		string		"_Building preview"
	}

	// ==================
	// content
	// ==================

	image building_pic
	{
		src		*cvar:mn_building_image
		preventratio	true
		pos			"310 58"
		size		"256 256"
		bgcolor		"0 0.08 0 1"
	}

	string title_building
	{
		string		*cvar:mn_building_name
		pos			"30 58"
		size		"300 25"
		font		"f_normal_bold"
	}

	string status_building
	{
		string		*cvar:mn_building_status
		pos			"30 190"
		size		"260 20"
		font		"f_small"
	}

	/* text using tabbed text */
	text buildinginfo
	{
		pos			"30 100"
		size		"300 80"
		lineheight	20
		tabwidth	140
		dataid		TEXT_BUILDING_INFO
		rows		"4"
	}

	custombutton ufopedia_link
	{
		icon		"icons/bordered_magnifying_glass"
		tooltip		"_UFOpaedia"
		pos			"260 190"
		size		"16 16"
		onClick		{ cmd "building_ufopedia;" }
	}

	func onWindowClosed
	{
		*node:buildings.building_preview@current = 0
	}
}
