// ==================
// HOSPITAL MENU
// ==================

component cmpHhumanoid_head extends button {
	color "1 0 0 1"
	pos			"0 0"
	size		"16 16"
}
component cmpHhumanoid_torso extends button {
	color "1 0 0 1"
	pos			"20 0"
	size		"16 16"
}
component cmpHhumanoid_arms extends button {
	color "1 0 0 1"
	pos			"40 0"
	size		"16 16"
}
component cmpHhumanoid_legs extends button {
	color "1 0 0 1"
	pos			"60 0"
	size		"16 16"
}
component cmpHdefault_body extends button {
	color "1 0 0 1"
	pos			"0 0"
	size		"16 16"
}

component cmpHospitalNamedTbar extends cmpDefNamedTbar {
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	image	bdr {
		pos "17 18"
	}
	tbar		bar {
		pos "19 18"
	}
	string	val {
		pos "160 0"
		longlines LONGLINES_PRETTYCHOP
	}
}

component cmpHospitalPanel extends panel {
	{
		size "300 85"
	}
	image button {
		src "ui/hosp_button_green"
		pos "0 0"
	}

	data index {
		integer		-1
	}

	button icon	{
		padding 0
		pos "248  11"
		size "40 40"
	}

	string item	{
		pos "15 10"
		color "1 1 1 1"
		size "220 16"
	}

	string rank	{
		pos "15 35"
		color "1 1 1 1"
		size "220 16"
	}

	bar health {
		pos "22 65"
		size "263 6"
		color "0 .8 0 1"
	}

	zone click {
		pos "0 0"
		size "300 85"
	}

	panel wounds {
		pos "150 30"
		size "225 20"
	}
}

window hospital
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src	"background/base_bg"
	}

	// ==================
	// Base header
	// ==================

	cmpBaseHeader baseHeader {
	}

	button bt_continue
	{
		tooltip	"_Back to baseview"
		background	"buttons/big_blue"
		pos		"950 20"
		size	"59 55"
		icon	"icons/return"
		onClick	{ cmd "ui_pop;" }
	}

	// ==================
	// employee list
	// ==================

	/**
	 * @param <1> Entry index
	 * @param <2> light, medium, serious
	 * @param <3> HP
	 * @param <4> MaxHP
	 * @param <5> Injury level
	 * @param <6> Name
	 * @param <7> Rank
	 */
	confunc hospitaladd	{
		call *node:root.panel@createChild("hos<1>", "cmpHospitalPanel")
		if ( <2> eq "light" ) {
			*node:root.panel.hos<1>.icon@icon				= "icons/health_normal"
			*node:root.panel.hos<1>.click@tooltip	= "_Slightly wounded"
		} elif ( <2> eq "medium" ) {
			*node:root.panel.hos<1>.icon@icon				= "icons/health_medium"
			*node:root.panel.hos<1>.click@tooltip	= "_Medium wounds"
		} elif ( <2> eq "serious" ) {
			*node:root.panel.hos<1>.icon@icon				= "icons/health_critical"
			*node:root.panel.hos<1>.click@tooltip	= "_Heavily wounded"
		}
		*node:root.panel.hos<1>.health@current = <3>
		*node:root.panel.hos<1>.health@max = <4>
		*node:root.panel.hos<1>.item@string = "<5>"
		*node:root.panel.hos<1>.rank@string = "<6>"
		*node:root.panel.hos<1>.index@integer = <1>
		*node:root.panel.hos<1>.click@onClick = { cmd "ui_push hospital_employee; hosp_empl_init <node:this.parent.index@integer>;" }
	}

	/**
	 * @param <1> Entry index
	 * @param <2> Body part type (id)
	 * @param <3> Severity of wounds
	 * @param <4> Tooltip
	 */
	confunc hospital_wounds {
		call *node:root.panel.hos<1>.wounds@createChild("wound_<2>", "cmpH<2>")
		if (<3> >= 0.3) {
			*node:root.panel.hos<1>.wounds.wound_<2>@icon = "icons/<2>_wound"
		} else {
			*node:root.panel.hos<1>.wounds.wound_<2>@icon = "icons/<2>_wound_treated"
		}
		*node:root.panel.hos<1>.wounds.wound_<2>@tooltip = "<4>"
	}

	panel panel {
		pos "20 95"
		size "980 648"
		layout		LAYOUT_COLUMN
		layoutColumns	3
		layoutMargin	5
		padding		20
		wheelscrollable	true

		onViewChange	{
			*node:parent.hos_scroll@fullsize = <fullsize>
			*node:parent.hos_scroll@current = <viewpos>
			*node:parent.hos_scroll@viewsize = <viewsize>
		}
	}
	vscrollbar hos_scroll {
		image		"ui/scrollbar_v_green"
		pos			"970 110"
		height		"628"
		onChange	{
			*node:parent.panel@viewpos = <current>
		}
	}

	// ==================
	// init function
	// ==================

	func onWindowOpened
	{
		call *node:root.panel@removeAllchild
		cmd "hosp_init;"
		*cvar:mn_lastsubmenu = "hospital"
	}
}

// ======================
// HOSPITAL EMPLOYEE MENU
// ======================
window hospital_employee
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src	"background/base_bg"
	}

	// ==================
	// Base header
	// ==================

	cmpBaseHeader baseHeader {
	}

	// ==================
	// actor info
	// ==================

	panel actorinfo {
		pos		"457 101"
		size	"555 471"
		background	"ui/panel_grey_green_large"
	}

	string name
	{
		pos		"475 118"
		size	"415 20"
	}

	model body
	{
		{
			anim	"stand2"
			angles	"0 70 90"
			scale	"6.7 6.7 6.7"
			pos		"480 180"
			size	"200 350"
			origin	"0 -35 0"
		}

		model head
		{
			tag		"tag_head"
			scale	"1.0 1.0 1.0"
		}
	}

	image rank {
		pos "475 470"
	}

	string rank_lbl {
		pos "475 175"
		size "200 20"
		longlines LONGLINES_PRETTYCHOP
	}

	cmpHospitalNamedTbar bar0 {
		{
			pos "690 182"
		}
		string lbl { string "_Strength:" }
	}

	cmpHospitalNamedTbar bar1 {
		{
			pos "690 212"
		}
		string lbl { string "_Speed:" }
	}

	cmpHospitalNamedTbar bar2 {
		{
			pos "690 242"
		}
		string lbl { string "_Accuracy:" }
	}

	cmpHospitalNamedTbar bar3 {
		{
			pos "690 272"
		}
		string lbl { string "_Mind:" }
	}

	cmpHospitalNamedTbar health  {
		{
			pos "690 432"
		}
		tbar bar
		{
			image "ui/bar_health"
		}
		string lbl
		{
			string "_Health:"
			longlines LONGLINES_PRETTYCHOP
		}
	}

	// ==================
	// action buttons
	// ==================

	panel buttonpanel {
		pos		"26 101"
		size	"276 471"
		background	"ui/panel_grey_green_large"
	}

	button button_hosp_bionic_start
	{
		string		"_Implants"
		color		"0 0.8 0 1"
		background		"ui/button_green_small"
		pos			"60 270"
		size		"207 48"
		font		"f_menu"
		tooltip		"_Not implemented"
		disabled	true
	}

	/**
	 * @param <1> Name
	 * @param <2> Body
	 * @param <3> BodySkin
	 * @param <4> Head
	 * @param <5> HeadSkin
	 * @param <6> HP
	 * @param <7> MaxHP
	 * @param <8> Rank
	 * @param <9> Rank image
	 */
	confunc hospital_employee_show {
		*node:root.name@string = "<1>"
		*node:root.body@src = "<2>"
		*node:root.body@skin = <3>
		*node:root.body.head@src = "<4>"
		*node:root.body.head@skin = <5>
		*node:root.health.val@string = "<6>"
		*node:root.health.bar@current = <6>
		*node:root.health.bar@max = <7>
		*node:root.rank_lbl@string = <8>
		*node:root.rank@src = <9>
	}

	/**
	 * @param <1> index
	 * @param <2> skill
	 * @param <3> skill description (Poor, Mediocre, Average, ...)
	 */
	confunc hospital_employee_set_values {
		*node:root.bar<1>.bar@current = "<2>"
		*node:root.bar<1>.val@string = "<3> (<2>)"
	}

	// ==================
	// init function
	// ==================

	func onWindowOpened
	{
		*cvar:mn_lastsubmenu = "hospital_employee"
	}
}
