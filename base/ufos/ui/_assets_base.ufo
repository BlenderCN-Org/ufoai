// Back to geoscape button
component Geoscape extends button
{
	pos				"-17 -17"
	size			"100 100"
	icon			"icons/earthlrg"
	background		"icons/circlelrg0"
	tooltip			"_Back"
	onMouseEnter	{ *node:this@background = "icons/circlelrg" }
	onMouseLeave	{ *node:this@background = "icons/circlelrg0" }
	onClick			{ cmd "ui_pop;" }
}

// Main panel button for base sections
component BaseSection extends panel
{
	{
		size		"298 48"
		onMouseEnter	{
			*node:this.bg@bgcolor = "0.527 0.6 0.21 0.4"
			*node:this.icon@background = "icons/circle"
		}
		onMouseLeave	{
			*node:this.bg@bgcolor = "0.527 0.6 0.21 0.2"
			*node:this.icon@background = "icons/circle0"
		}
	}
	panel bg
	{
		{
			pos		"0 8"
			size	"298 32"
			bgcolor	"0.527 0.6 0.21 0.2"
			ghost	true
		}
	}
	button icon
	{
		pos			"12 0"
		size 		"48 48"
		background	"icons/circle0"
		ghost		true
		// else the button is disabled
		onClick		{ }
	}
	string label
	{
		pos				"75 8"
		size			"170 32"
		contentalign	ALIGN_CL
		color			"0.56 0.81 0.76 1"
		ghost			true
	}
}

// Stats bar for building space
component BuildingSpace extends panel
{
	{
		size	"298 30"
		onMouseEnter
		{
			*node:this.label@color = "0.56 0.81 0.76 1"
			*node:this.data@color = "0.56 0.81 0.76 1"
			*node:this.alert@icon = "icons/windowplusb_hovered"
		}
		onMouseLeave
		{
			*node:this.label@color = "0.56 0.81 0.76 0.5"
			*node:this.data@color = "0.56 0.81 0.76 0.5"
			*node:this.alert@icon = "icons/windowplusb"
		}
		onClick			{ cmd "buildings_click <node:this.id@string>;" }
	}
	data id {}
	button smlicon
	{
		pos		"16 0"
		size	"17 17"
		ghost	true
	}
	string label
	{
		string			""
		pos				"35 0"
		size			"120 20"
		font			"f_verysmall"
		contentalign	ALIGN_LL
		color			"0.56 0.81 0.76 0.7"
		ghost			true
	}
	string data
	{
		string			""
		pos				"155 0"
		size			"107 20"
		font			"f_verysmall"
		contentalign	ALIGN_LR
		color			"0.56 0.81 0.76 0.7"
		ghost			true
	}
	texture data_bg
	{
		pos			"16 18"
		size		"246 6"
		src			"ui/bar_bg"
		ghost		true
	}
	bar data_bar
	{
		pos			"16 18"
		size		"246 6"
		color		"0.56 0.81 0.76 1"
		current		0
		max			1
		readonly	true
		ghost		true
	}
	button alert
	{
		icon			"icons/windowplusb"
		pos				"266 12"
		size			"18 18"
//		onClick			{ cmd "buildings_click <node:parent.id@string>;" }
		ghost		true
	}
}

// Small base selector layout
component BaseLayoutMini extends baselayout
{
	pos				"0 0"
	size			"16 16"
	color			"0.56 0.81 0.76 0.5"
	bgcolor			"0.56 0.81 0.76 0.1"
	padding			1
	onMouseEnter	{ *node:this@color = "0.56 0.81 0.76 1" }
	onMouseLeave	{ *node:this@color = "0.56 0.81 0.76 0.5" }
	onClick			{ cmd "ui_pop; mn_base_select <baseid>;" }
}


// Aircraft image/button
component AircraftIcon extends panel
{
	{
		pos		"0 0"
		size		"48 48"
	}
	button icon
	{
		pos		"0 0"
		size	"48 48"
	}
	data aircraft_id { }
}

// Research/Production
component ProgressBar extends panel
{
	{
		pos			"0 0"
		size 		"250 28"
	}
	string label
	{
		pos				"0 0"
		size			"250 20"
		font			"f_verysmall"
		contentalign	ALIGN_LL
		color			"0.56 0.81 0.76 1"
		ghost			true
	}
	texture data_bg
	{
		pos			"0 20"
		size		"224 6"
		src			"ui/bar_bg"
		ghost		true
	}
	bar data_bar
	{
		pos			"0 20"
		size		"224 6"
		color		"0.56 0.81 0.76 1"
		current		0
		max			100
		ghost		true
	}
}


// Employee count
component EmployeeCount extends panel
{
	{
		size	"110 48"
	}
	button icon
	{
		pos		"0 0"
		size	"48 48"
		ghost	true
	}
	string data
	{
		pos				"36 0"
		size			"74 48"
		color			"0.56 0.81 0.76 1"
		font			"f_small_bold"
		contentalign	ALIGN_CL
		ghost			true
	}
	string label
	{
		pos				"36 27"
		size			"74 17"
		color			"0.56 0.81 0.76 1"
		font			"f_verysmall"
		contentalign	ALIGN_UL
		ghost			true
	}
}

// Alien in alien containment
component AlienPanel extends panel
{
	{
		size 			"425 64"
		border			"2"
		bordercolor		"0.56 0.81 0.76 1"
		bgcolor			"0.56 0.81 0.76 0.1"
		onClick			{ cmd "aliencont_click <node:this.alien_num@string>; select_alien <node:this.alien_num@string>;" }
	}
	button action
	{
		pos				"366 6"
		size			"48 48"
	}
	string label
	{
		pos				"6 6"
		size			"360 20"
		color			"0.56 0.81 0.76 1"
		font			"f_small_bold"
		contentalign	ALIGN_CL
		ghost			true
	}
	string status
	{
		pos				"6 26"
		size			"360 20"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		ghost			true
	}
	texture data_bg
	{
		pos			"8 48"
		size		"409 8"
		src			"ui/bar_bg"
		ghost		true
	}
	bar data_bar
	{
		pos			"8 48"
		size		"408 8"
		color		"0.56 0.81 0.76 1"
		current		0
		max			100
		ghost		true
	}
	data alien_num { string "" }
}

// Research item generic(items researched)
component ResearchItemGeneric extends panel
{
	{
		pos				"0 0"
		size 			"713 28"
		bgcolor			"0.56 0.81 0.76 0.1"
		onMouseEnter	{ *node:this@bgcolor = "0.56 0.81 0.76 0.2" }
		onMouseLeave	{ *node:this@bgcolor = "0.56 0.81 0.76 0.1" }
		onClick			{
			cmd "ui_research_select <node:this.id@string>;"
//			call *node:root.base_container.mainBody.techlist_border.techs@removeAllChild
//			call *node:root.base_container.research_ext.techs_ext@removeAllChild
		}
	}
	data id {
		integer		-1
		string		""
    }
	bar status
	{
		pos		"0 0"
		size	"713 28"
		color	"0.56 0.81 0.76 0.3"
		current 0
		max		100
		ghost	true
	}
	button ufopaedia
	{
		icon	"icons/windowinfo"
		pos		"5 5"
		size	"18 18"
		/*
		 * This onClick is an ugly workaround because research_select calls research_init,
		 * which would produce duplicates of the tech entries if the existing entries
		 * weren't removed. The code destroying the nodes must be called AFTER
		 * research_select so that the integer can be retrieved, but research_select
		 * does not actually process until AFTER the nodes because (I believe) it
		 * doesn't get called until the next frame. Because of how the timing works,
		 * this onClick code can not be placed into a confunc because all commands
		 * would be processed in the order they are called. This means we couldn't
		 * slip the @removeAllChild commands in between the time when research_select
		 * is called and when it is executed.
		 * @sa ExtResearchItem.ufopaedia@onClick
		 */
		onClick {
			cmd "research_select <node:parent.id@integer>;"
			cmd "mn_show_ufopedia;"
//			call *node:root.base_container.mainBody.techlist_border.techs@removeAllChild
//			call *node:root.base_container.research_ext.techs_ext@removeAllChild
		}
	}
	string title
	{
		pos				"30 0"
		size			"480 28"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		ghost			true
	}
}

// Research item (items researched in base)
component ResearchItem extends ResearchItemGeneric
{
    {}
	string scientists
	{
		pos				"560 0"
		size			"35 28"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		ghost			true
	}
	button remove_scientist
	{
		icon	"icons/windowminusb"
		pos		"600 5"
		size	"18 18"
		onClick			{
			cmd "ui_research_select <node:parent.id@string>; ui_research_change <node:parent.id@string> -1;"
//			call *node:root.base_container.mainBody.techlist_border.techs@removeAllChild
//			call *node:root.base_container.research_ext.techs_ext@removeAllChild
			cmd "get_lab_space_string;"
		}
	}
	button add_scientist
	{
		icon	"icons/windowplusb"
		pos		"625 5"
		size	"18 18"
		onClick			{
			cmd "ui_research_select <node:parent.id@string>; ui_research_change <node:parent.id@string> 1;"
//			call *node:root.base_container.mainBody.techlist_border.techs@removeAllChild
//			call *node:root.base_container.research_ext.techs_ext@removeAllChild
			cmd "get_lab_space_string;"
		}
	}
}

// External research item (items researched in other bases)
component ExtResearchItem extends ResearchItemGeneric
{
    {}
	string base
	{
		pos				"560 0"
		size			"130 28"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		ghost			true
	}
}

// Market category label
component MarketCategory extends panel
{
	{
		size 			"713 28"
		bgcolor			"0.56 0.81 0.76 0.3"
		onMouseEnter	{ *node:this@bgcolor = "0.56 0.81 0.76 0.4" }
		onMouseLeave	{ *node:this@bgcolor = "0.56 0.81 0.76 0.3" }
	}
	string name
	{
		pos				"30 0"
		size			"400 28"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		ghost			true
	}
}

// Market category list
component MarketList extends panel
{
	size			"713 0"
	layout			LAYOUT_TOP_DOWN_FLOW
	layoutMargin	2
}
// Market item
component MarketItem extends panel
{
	{
		pos				"0 0"
		size 			"713 28"
		bgcolor			"0.56 0.81 0.76 0.1"
		onMouseEnter	{ *node:this@bgcolor = "0.56 0.81 0.76 0.15" }
		onMouseLeave	{ *node:this@bgcolor = "0.56 0.81 0.76 0.1" }
		onClick			{ cmd "ui_market_select <node:this.id@string>;" }
	}
	data id { }
	data rate { integer 1 }

	// UFOPaedia link for each item
	button ufopaedia
	{
		icon			"icons/windowinfo"
		tooltip			"_View UFOPaedia entry"
		pos				"5 5"
		size			"18	18"
		onClick			{
			cmd "ui_market_showinfo \"<node:parent.id@string>\";"
			cmd "market_openpedia;"
		}
	}
	string name
	{
		pos				"30 0"
		size			"290 28"
		color			"0.56 0.81 0.76 0.7"
		contentalign	ALIGN_CL
		ghost			true
	}
	// Count of items in base
	string base
	{
		pos				"320 0"
		size			"80 28"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CR
		ghost			true
	}
	// Buy items
	spinner buy {
		pos					"406 6"
		size				"74 16"
		topIcon				"icons/arrowtext_lft"
		mode				SPINNER_ONLY_INC
		delta				1
		shiftincreasefactor	10.0
		onMouseEnter		{ *node:this@topIcon = "icons/arrowtext_lft0" }
		onMouseLeave		{ *node:this@topIcon = "icons/arrowtext_lft" }
		onChange			{
			cmd "ui_market_buy \"<node:parent.id@string>\" <lastdiff>;"
			cmd "ui_market_fill <cvar:ui_market_category>;"
			cmd "ui_market_select \"<node:parent.id@string>\";"
		}
	}
	string buy_price {
		pos				"406 6"
		size			"74 16"
		color			"0 0 0 1"
		font			"f_verysmall_bold"
		contentalign	ALIGN_CR
		ghost			true
	}
	checkbox autosell
	{
		tooltip				"_Lock current stock level"
		current				0
		iconChecked			"icons/windowlock"
		iconUnchecked		"icons/windowlock_light"
		iconIndeterminate	"icons/windowlock_light" // @TODO: What does this do?
		pos					"484 5"
		size				"18 18"
		invis				false
		onMouseEnter		{ *node:this@iconUnchecked = "icons/windowlock" }
		onMouseLeave		{ *node:this@iconUnchecked = "icons/windowlock_light" }
		onClick				{ cmd "ui_market_setautosell \"<node:parent.id@string>\" <node:this@string>;"}
	}
	// Sell items
	spinner sell {
		pos					"508 6"
		size				"74 16"
		topIcon				"icons/arrowtext_rgt"
		mode				SPINNER_ONLY_DEC
		delta				1
		shiftincreasefactor	10.0
		onMouseEnter		{ *node:this@topIcon = "icons/arrowtext_rgt0" }
		onMouseLeave		{ *node:this@topIcon = "icons/arrowtext_rgt" }
		onChange			{
			cmd "ui_market_buy \"<node:parent.id@string>\" <lastdiff>;"
			cmd "ui_market_fill <cvar:ui_market_category>;"
			cmd "ui_market_select \"<node:parent.id@string>\";"
		}
	}
	string sell_price {
		pos				"508 6"
		size			"74 16"
		color			"0 0 0 1"
		font			"f_verysmall_bold"
		contentalign	ALIGN_CL
		ghost			true
	}
	// Count of items in the market
	string market
	{
		pos				"586 0"
		size			"100 28"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		ghost			true
	}
}

// Base name and container panel
component BaseContainerPanel extends panel
{
	{
		pos				"5 5"
		size			"723 758"
		border			"2"
		bordercolor		"0.56 0.81 0.76 1"
	}

	// @TODO: make next/prev base btns work
	button prev
	{
		icon			"icons/windowprev"
		tooltip			"_Previous base"
		pos				"90 6"
		size			"18	18"
		onClick			{ cmd "ui_pop; mn_base_prev;" }
		onWheelUp		{ cmd "ui_pop; mn_base_next;" }
		onWheelDown		{ cmd "ui_pop; mn_base_prev;" }
	}
	button next
	{
		icon			"icons/windownext"
		tooltip			"_Next base"
		pos				"115 6"
		size			"18	18"
		onClick			{ cmd "ui_pop; mn_base_next;" }
		onWheelUp		{ cmd "ui_pop; mn_base_next;" }
		onWheelDown		{ cmd "ui_pop; mn_base_prev;" }
	}
	// Base name
	textentry name
	{
		string		*cvar:mn_base_title
		pos			"135 0"
		size		"465 32"
		font		"f_small_bold"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		onChange 	{ cmd "base_changename" } 	// stores the value of the cvar in the base structure
	}
	// Base selector
	panel base_selector
	{
		{
			pos		"440 7"
			size	"160 16"
			layout			LAYOUT_COLUMN
			layoutColumns	8
			layoutMargin	4
		}
		BaseLayoutMini base0 { baseid 0 }
		BaseLayoutMini base1 { baseid 1 }
		BaseLayoutMini base2 { baseid 2 }
		BaseLayoutMini base3 { baseid 3 }
		BaseLayoutMini base4 { baseid 4 }
		BaseLayoutMini base5 { baseid 5 }
		BaseLayoutMini base6 { baseid 6 }
		BaseLayoutMini base7 { baseid 7 }
	}
	string credits
	{
		string		*cvar:mn_credits
		pos			"600 0"
		size		"120 32"
		font		"f_small_bold"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CR
	}
}

// Base name and container panel with body and title
component BaseContainerPanelV2 extends BaseContainerPanel
{
	panel mainBody
	{
		{
			pos				"4 30"
			size			"715 534"
			border			"2"
			bordercolor		"0.527 0.6 0.21 1"
		}
		// Title bar
		panel header
		{
			{
				pos			"1 1"
				size		"713 32"
				bgcolor		"0.527 0.6 0.21 0.2"
			}
			string title
			{
				string			"Dummy"
				pos				"90 1"
				size			"593 32"
				contentalign	ALIGN_CL
				color			"0.56 0.81 0.76 1"
			}
			button back
			{
				icon			"icons/windownext"
				tooltip			"_Back to base"
				pos				"683 7"
				size			"18	18"
				onClick			{ cmd "ui_pop;" }
			}
		}
	}
}

window BaseMgmtWindow
{
	{ size	"1024 768" }

	BaseContainerPanelV2 base_container {}

	// Back to geoscape
	Geoscape geoscape_button { }

}
