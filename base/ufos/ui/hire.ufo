// ==================
// EMPLOYEES MENU
// ==================

component cmpSelect extends checkbox_ui2
{
		current		0
		size		"20 20"
		onChange	{ cmd "employee_hire <num>; employee_equipcheck;" }
		onRClick		{ cmd "employee_delete <num>; employee_equipcheck;" }
}

window employees
{
	// Base name and container panel
	BaseContainerPanelV2 base_container
	{
		// hireand fire panel
		panel mainBody
		{
			// Title bar
			panel header
			{
				{}
				string title
				{
					string			"_Hire and Fire"
				}
			}

            panel employees
            {
                /** @todo Remove me */
                {
                    size		"535 540"
                } // empty properties to fix parsing problem with "image" token (behaviour or property?)


                // ==================
                // tabs
                // ==================

                panel tab_background {
                    pos		"14 46"
                    size	"556 56"
                }

                tab empl_switch {
                    {
                        cvar		*cvar:mn_hiretype
                        pos			"27 36"
                        size		"528 39"
                        onChange {
                            cmd "employee_init <cvar:mn_hiretype>;"
                            *node:parent.employees.employee_list@lineselected = 0
                            *node:parent.employee_list@viewpos = 0
                            *node:parent.employees.scroll@current = 0
                        }
                    }
                    option soldier {
                        label	"_Soldiers"
                        value	"0"
                        icon	"icons/smallhead_soldier"
                    }
                    option pilot {
                        label	"_Pilots"
                        value	"3"
                        icon	"icons/smallhead_pilot"
                    }
                    option worker {
                        label	"_Workers"
                        value	"2"
                        icon	"icons/smallhead_worker"
                    }
                    option scientist {
                        label	"_Scientists"
                        value	"1"
                        icon	"icons/smallhead_scientist"
                    }
                }


                // ==================
                // names list
                // ==================

                rows list_background {
                    pos			"29 80"
                    size		"370 540"
                    lineheight	38
                }

                panel scroll_background {
                    pos			"363 85"
                    size		"40 540"
                    bgcolor		"0 0 0 0.5"
                }

                panel list_border {
                    pos		        "15 75"
                    size	        "393 565"
                    padding		    6
                    layout		    LAYOUT_TOP_DOWN_FLOW
                    layoutMargin    2
                }

                /**
                 * @brief Disable checkbox (employee is away from base)
                 * @param[in] <1> row of the employee in the view
                 */
                confunc employeedisable
                {
                    *node:parent.bt_employee<1>@current = 1
                    *node:parent.bt_employee<1>@invis = false
                    *node:parent.bt_employee<1>@disabled = true
                    *node:parent.bt_employee<1>@tooltip = "_Employee is away"
                }

                /**
                 * @brief Enable checked checkox (Employee can be fired)
                 * @param[in] <1> row of the employee in the view
                 */
                confunc employeefire
                {
                    *node:parent.bt_employee<1>@current = 1
                    *node:parent.bt_employee<1>@invis = false
                    *node:parent.bt_employee<1>@disabled = false
                    *node:parent.bt_employee<1>@tooltip = "_Unhire - Right click to remove"
                }

                /**
                 * @brief Enable unchecked checkox (Employee can be hired)
                 * @param[in] <1> row of the employee in the view
                 */
                confunc employeehire
                {
                    *node:parent.bt_employee<1>@current = 0
                    *node:parent.bt_employee<1>@invis = false
                    *node:parent.bt_employee<1>@disabled = false
                    *node:parent.bt_employee<1>@tooltip = "_Hire - Right click to remove"
                }

                /**
                 * @brief Hide checkox (Empty slot)
                 * @param[in] <1> row of the employee in the view
                 */
                confunc employeehide
                {
                    *node:parent.bt_employee<1>@invis = true
                }

                confunc employee_equipcheck {
                    if ( *cvar:mn_hiresoldiers > 0 ) {
                        *node:employees.bt_equipsoldiers@disabled = false
                    } else {
                        *node:employees.bt_equipsoldiers@disabled = true
                    }
                }

                cmpSelect bt_employee0
                {
                    num 0
                    pos "325 90"
                }

                cmpSelect bt_employee1
                {
                    num 1
                    pos "325 128"
                }

                cmpSelect bt_employee2
                {
                    num 2
                    pos "325 166"
                }

                cmpSelect bt_employee3
                {
                    num 3
                    pos "325 204"
                }

                cmpSelect bt_employee4
                {
                    num 4
                    pos "325 242"
                }

                cmpSelect bt_employee5
                {
                    num 5
                    pos "325 280"
                }

                cmpSelect bt_employee6
                {
                    num 6
                    pos "325 318"
                }

                cmpSelect bt_employee7
                {
                    num 7
                    pos "325 356"
                }

                cmpSelect bt_employee8
                {
                    num 8
                    pos "325 394"
                }

                cmpSelect bt_employee9
                {
                    num 9
                    pos "325 432"
                }

                cmpSelect bt_employee10
                {
                    num 10
                    pos "325 470"
                }

                cmpSelect bt_employee11
                {
                    num 11
                    pos "325 508"
                }

                cmpSelect bt_employee12
                {
                    num 12
                    pos "325 546"
                }

                cmpSelect bt_employee13
                {
                    num 13
                    pos "325 594"
                }

                /**
                 * @param[in] <1> number of employee in the list
                 */
                confunc hire_update_number {
                    *node:parent.scroll@fullsize = <1>
                }

                confunc hire_fix_scroll {
                    *node:parent.scroll@current = <1>
                    *node:parent.list_background@current = <1>
                }

                confunc hire_select {
                    *node:parent.employee_list@lineselected = <1>
                }

                textlist employee_list
                {
                    dataid		    TEXT_LIST
                    pos			    "45 80"
                    width		    280
                    lineheight	    38
                    tabwidth	    100
                    rows		    "14"
                    color		    "0.56 0.81 0.76 1"
                    selectcolor	    "1 1 1 1"
                    onClick			{ cmd "employee_select <lineselected>;" }
                    onViewChange	{
                        *node:parent.scroll@fullsize = <fullsize>
                        *node:parent.scroll@current = <viewpos>
                        *node:parent.scroll@viewsize = <viewsize>
                        cmd "employee_scroll <viewpos>;"
                    }
                }

                vscrollbar scroll
                {
                    image		"ui/scrollbar_v_green"
                    pos			"368 85"
                    height		"514"
                    current		0
                    viewsize	15
                    fullsize	50
                    onChange	{
                        *node:parent.employee_list@viewpos = <current>
                        cmd "employee_scroll <current>;"
                    }
                }
            }
        }
    }

	// ==================
	// actor info
	// ==================


	panel actor_info {
		{
			pos		"731 434"
			size	"290 328"
		}
	}

	textentry name
	{
		visiblewhen "*cvar:mn_show_employee >= 1"
		string		*cvar:mn_name
        tooltip		"_Rename your unit"
		pos			"732 415"
		size		"290 20"
        color		"0.56 0.81 0.76 1"
		onChange	{ cmd "employee_changename;" }
	}

	image	rank_img
	{
		visiblewhen "*cvar:mn_show_employee >= 1"
		src		*cvar:mn_chrrank_img
		pos			"735 440"
        size		"110 110"
	}

	model body
	{
		{
			visiblewhen "*cvar:mn_show_employee >= 1"
			src			*cvar:mn_body
			skin		*cvar:mn_body_skin
			anim		"stand2"
			angles		"0 70 90"
			scale		"5.1 5.1 5.1"
			pos			"790 432"
			size		"200 290"
			origin		"0 -40 0"
		}

		model head
		{
			visiblewhen "*cvar:mn_show_employee >= 1"
			src			*cvar:mn_head
			skin		*cvar:mn_head_skin
			tag			"tag_head"
			scale		"1.0 1.0 1.0"
		}
	}

	string rank_lbl
	{
		visiblewhen "*cvar:mn_show_employee >= 1"
		string		*cvar:mn_chrrank
		pos			"745 696"
		size		"200 20"
	}


	panel soldier_info {
		{
			visiblewhen "*cvar:mn_show_employee == 1"
			pos		"735 440"
			size	"285 320"
		}


		string	missions_lbl { pos "10 278" size "170 20" string "_Missions Performed:" }
		string	missions_val { pos "190 278" size "90 20" string *cvar:mn_chrmis contentalign ALIGN_UR }

		string	kills_lbl { pos "10 298" size "170 20" string "_Enemies Killed:" }
		string	kills_val { pos "190 298" size "90 20" string *cvar:mn_chrkillalien contentalign ALIGN_UR }
	}

    MainMenu3Btn bt_equipsoldiers
    {
        string      "_Equip soldiers"
        pos		    "760 375"
        size	    "198 30"
        font		"f_small_bold"
        color		"0 0 0 0.9"
        selectcolor	"0 0 0 1"
        onClick	{
            cmd "ui_push equipment -1;"
        }
    }

	// ==================
	// buttons
	// ==================

	panel global_info {
		{
			pos		"740 10"
			size	"250 90"
		}
		string label_people {
			string	"_Occupation"
			font	"f_normal_bold"
			pos		"10 08"
			size	"140 20"
			contentalign	ALIGN_UC
		}
		string number_people {
			string	*cvar:mn_hirepeople
			font	"f_normal_bold"
			pos		"150 08"
			size	"100 20"
			contentalign	ALIGN_UC
		}

		button icon_soldier {
			icon	"icons/head_soldier"
			pos		"10 28"
			size	"42 42"
		}
		string number_soldier {
			string	*cvar:mn_hiresoldiers
			pos		"12 68"
			size	"42 20"
			contentalign	ALIGN_UR
		}

		button icon_pilot {
			icon	"icons/head_pilot"
			pos		"70 28"
			size	"42 42"
		}
		string number_pilot {
			string	*cvar:mn_hirepilots
			pos		"72 68"
			size	"42 20"
			contentalign	ALIGN_UR
		}

		button icon_worker {
			icon	"icons/head_worker"
			pos		"130 28"
			size	"42 42"
		}
		string number_worker {
			string	*cvar:mn_hireworkers
			pos		"132 68"
			size	"42 20"
			contentalign	ALIGN_UR
		}

		button icon_scientist {
			icon	"icons/head_scientist"
			pos		"190 28"
			size	"42 42"
		}
		string number_scientist {
			string	*cvar:mn_hirescientists
			pos		"192 68"
			size	"42 20"
			contentalign	ALIGN_UR
		}
	}

	// Abilities panel
	// @TODO: Remove "mediocre" dsc text. Look at CL_ActorGetSkillString, cl_team.c
	panel abilities
	{
		{
			visiblewhen "*cvar:mn_show_employee == 1"
			pos			"740 100"
			size		"250 125"
			ghost		true
		}
		StatBarWide strength
		{
			{
				pos		"0    0"
			}
			string label { string "_Strength" }
			bar valuebar { current *cvar:mn_vpwr }
			string valuenum { string *cvar:mn_tpwr }
		}
		StatBarWide speed
		{
			{
				pos		"0 30"
			}
			string label { string "_Speed" }
			bar valuebar { current *cvar:mn_vspd }
			string valuenum { string *cvar:mn_tspd }
		}
		StatBarWide accuracy
		{
			{
				pos		"0 60"
			}
			string label { string "_Accuracy" }
			bar valuebar { current *cvar:mn_vacc }
			string valuenum { string *cvar:mn_tacc }
		}
		StatBarWide mind
		{
			{
				pos		"0 90"
			}
			string label { string "_Mind" }
			bar valuebar { current *cvar:mn_vmnd }
			string valuenum { string *cvar:mn_tmnd }
		}
	}
	// Skills panel
	panel skills
	{
		{
			visiblewhen "*cvar:mn_show_employee == 1"
			pos			"740 220"
			size		"250 155"
			ghost		true
		}
		StatBarWide cls
		{
			{
				pos		"0 0"
			}
			string label { string "_CQB" tooltip "_Close Quarters Battle" }
			bar valuebar { current *cvar:mn_vcls }
			string valuenum { string *cvar:mn_tcls }
		}
		StatBarWide ass
		{
			{
				pos		"0 30"
			}
			string label { string "_Assault" }
			bar valuebar { current *cvar:mn_vass }
			string valuenum { string *cvar:mn_tass }
		}
		StatBarWide snp
		{
			{
				pos		"0 60"
			}
			string label { string "_Sniper" }
			bar valuebar { current *cvar:mn_vsnp }
			string valuenum { string *cvar:mn_tsnp }
		}
		StatBarWide exp
		{
			{
				pos		"0 90"
			}
			string label { string "_Explosives" }
			bar valuebar { current *cvar:mn_vexp }
			string valuenum { string *cvar:mn_texp }
		}

	}
	// Pilot Skills panel
	panel skillspilot
	{
		{
			visiblewhen "*cvar:mn_show_employee == 2"
			pos			"740 250"
			size		"250 155"
			ghost		true
		}
		StatBarWide pil
		{
			{
				pos		"0 0"
			}
			string label { string "_Piloting" }
			bar valuebar { current *cvar:mn_vpil }
			string valuenum { string *cvar:mn_tpil }
		}
		StatBarWide tar
		{
			{
				pos		"0 30"
			}
			string label { string "_Targeting" }
			bar valuebar { current *cvar:mn_vtar }
			string valuenum { string *cvar:mn_ttar }
		}
		StatBarWide evad
		{
			{
				pos		"0 60"
			}
			string label { string "_Evading" }
			bar valuebar { current *cvar:mn_vevad }
			string valuenum { string *cvar:mn_tevad }
		}
	}
	// Health panel
	panel health
	{
		{
			visiblewhen "*cvar:mn_show_employee <= 2"
			pos			"740 340"
			size		"250 30"
			ghost		true
		}
		StatBarWide hp
		{
			{
				pos		"0 0"
			}
			string label { string "_Health" tooltip "_<Healthpoints" }
			bar valuebar { current *cvar:mn_vhp max *cvar:mn_vhpmax}
			string valuenum { string *cvar:mn_thp }
		}
    }


	// ==================
	// init function
	// ==================

	func onWindowOpened
	{
		*cvar:mn_lastsubmenu = "employees"
		*node:root.base_container.mainBody.employees.employee_list@viewpos = 0
		*node:root.base_container.mainBody.employees.employee_list@lineselected = 0
		*node:root.base_container.mainBody.employees.scroll@current = 0
		cmd "employee_init 0;"
		*cvar:mn_hiretype = 0
		cmd "employee_update_count; employee_equipcheck;"
	}

	func onWindowClosed
	{
		// call base init function because the amount of workers, scientists and
		// so on can also change the button state in the base view
		cmd "base_init;"
	}

	// Fuzzy screen effect (see assets.ufo)
	screen overlay { }
}
