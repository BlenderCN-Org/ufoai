// ==================
// UFOPEDIA MENU
// ==================

window ufopedia
{
	{
		pos			"10 50"
		size		"1000 619"
		dragbutton	true
		closebutton	true
		image		ui/popup_alpha_tile
		string		"_Ufopedia"
		modal		true				// we can't click out
	}

	string nameentry
	{
		string		*cvar:mn_uptitle
		pos			"33 59"
		size		"298 39"
		font		f_small_bold
	}

	panel navigation_panel {
		pos		"349 87"
		size	"628 510"
		image	"ui/panel_green_tab"
	}

	tab navigation {
		cvar	*cvar:ufopedia_content
		pos		"350 55"
		size	"622 43"
		
		option description {
			label	"_Description"
			value	"description"
		}
		option preresearch {
			label	"_Pre research text"
			value	"preresearch"
		}
		option stats {
			label	"_Statistics"
			value	"stats"
		}
		onChange {
			cmd "update_ufopedia_layout"
		}
	}

	todo todounneedtab {
		pos		"350 45"
		string	"We must hide/disable unneed tabs"
	}

	todo todoweaponinfo {
		pos		"370 45"
		string	"Weapon info are missing???"
	}

	todo todopreresearch {
		pos		"390 45"
		string	"Is description/preresearch working?"
	}

	todo todorequiremement {
		pos		"410 45"
		string	"Can we move requirement into the description tab (under the image)?"
	}

	// ==================
	// summary
	// ==================

	panel summary {
		{
			pos		"27 95"
			size	"307 501"
			image	"ui/panel_green"
		}
		
		optiontree ufopedia_summary
		{
			cvar		*cvar:ufopedia_element
			dataid		OPTION_UFOPEDIA
			pos			"10 10"
			size		"257 484"
			font		f_small
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			onChange	{
				cmd "ufopedia_click <cvar:ufopedia_element>"
				*node:(abstractscrollbar)root.ufopedia_scroll@current 0
				*node:(text)root.ufopedia@viewpos 0
				cmd "update_ufopedia_layout"
			}
			onViewChange	{
				*node:(abstractscrollbar)parent.ufopedia_summary_scroll@current <viewpos>
				*node:(abstractscrollbar)parent.ufopedia_summary_scroll@fullsize <fullsize>
				*node:(abstractscrollbar)parent.ufopedia_summary_scroll@viewsize <viewsize>
			}
		}
	
		vscrollbar ufopedia_summary_scroll {
			image		ui/scrollbar_v_green
			pos			"277 10"
			height		"484"
			current		0
			viewsize	16
			fullsize	50
			onChange	{ *node:parent.ufopedia_summary@viewpos <current> }
		}
		
	}

	// ==================
	// deco
	// ==================
	
	pic image_top
	{
		image		*cvar:mn_upimage_top
		pos			"782 120"
		size		"153 341"
		preventratio	true
	}

	model mdl_top
	{
		model		*cvar:mn_upmodel_top
		angles		"0 0 120"
		pos			"783 119"
		size		"151 151"
		autoscale	true
		rotatewithmouse	true
	}

	model mdl_bottom
	{
		model		*cvar:mn_upmodel_bottom
		angles		"0 0 120"
		pos			"797 321"
		size		"120 120"
		autoscale	true
		rotatewithmouse	true
	}

	// ==================
	// text and info
	// ==================

	text ufopedia
	{
		dataid		TEXT_UFOPEDIA
		pos			"364 110"
		size		"406 473"
		font		f_small
		lineheight	18
		tabwidth	100
		rows		22
		onViewChange	{
			*(abstractscrollbar)ufopedia_scroll@fullsize <lines>
			*(abstractscrollbar)ufopedia_scroll@current <viewpos>
			*(abstractscrollbar)ufopedia_scroll@viewsize <viewsize>
		}
	}

	vscrollbar ufopedia_scroll {
		image		ui/scrollbar_v_green
		pos			"953 105"
		height		"473"
		current		0
		viewsize	16
		fullsize	50
		onChange	{ *ufopedia@viewpos <current> }
	}

	text description
	{
		dataid		TEXT_STANDARD
		pos			"368 116"
		size		"412 200"
		lineheight	20
		tabwidth	230
		rows		"10"
		onViewChange	{
			*(abstractscrollbar)description_scroll@fullsize <lines>
			*(abstractscrollbar)description_scroll@current <viewpos>
			*(abstractscrollbar)description_scroll@viewsize <viewsize>
		}
	}

	vscrollbar description_scroll {
		image		ui/scrollbar_v_green
		pos			"780 116"
		height		"200"
		current		0
		viewsize	10
		fullsize	10
		hidewhenunused true
		onChange	{ *description@viewpos <current> }
	}

	text techtree
	{
		dataid		TEXT_LIST
		pos			"363 326"
		size		"334 246"
		lineheight	"20"
		rows		"12"
		mousefx		true
		onClick		{ cmd "techtree_click <lineselected>" }
		onViewChange	{
			*(abstractscrollbar)techtree_scroll@fullsize <lines>
			*(abstractscrollbar)techtree_scroll@current <viewpos>
			*(abstractscrollbar)techtree_scroll@viewsize <viewsize>
		}
	}

	vscrollbar techtree_scroll {
		image		ui/scrollbar_v_green
		pos			"780 326"
		height		"246"
		current		0
		viewsize	12
		fullsize	12
		hidewhenunused true
		onChange	{ *techtree@viewpos <current> }
	}

	// ==================
	// weapon info
	// @todo never displayed??? can we fix something
	// ==================

	pic weapon_dec
	{
		visiblewhen "*cvar:mn_changeweapon == 1"
		tooltip		"_Previous weapon chambered for this ammunition"
		image		ui/buttons_small
		pos			"270 704"
		texl		"0 0"
		texh		"16 16"
		mousefx		true
		onClick		{ cmd "mn_decreaseweapon;" }
	}

	pic ammo_dec
	{
		visiblewhen "*cvar:mn_changeweapon == 2"
		tooltip		"_Previous ammunition compatible with this weapon"
		image		ui/buttons_small
		pos			"270 704"
		texl		"0 0"
		texh		"16 16"
		mousefx		true
		onClick		{ cmd "mn_decreaseweapon;" }
	}

	pic weapon_inc
	{
		visiblewhen "*cvar:mn_changeweapon == 1"
		tooltip		"_Next weapon chambered for this ammunition"
		image		ui/buttons_small
		pos			"510 704"
		texl		"16 0"
		texh		"32 16"
		mousefx		true
		onClick		{ cmd "mn_increaseweapon;" }
	}

	pic ammo_inc
	{
		visiblewhen "*cvar:mn_changeweapon == 2"
		tooltip		"_Next ammunition compatible with this weapon"
		image		ui/buttons_small
		pos			"510 704"
		texl		"16 0"
		texh		"32 16"
		mousefx		true
		onClick		{ cmd "mn_increaseweapon;" }
	}

	string header_weapon
	{
		visiblewhen "*cvar:mn_displayweapon == 1"
		string		"_With weapon:"
		pos			"115 704"
		size		"150 20"
		textalign	cl
		font		f_small
	}

	string header_ammo
	{
		visiblewhen "*cvar:mn_displayweapon == 2"
		string		"_With ammo:"
		pos			"115 704"
		size		"150 20"
		textalign	cl
		font		f_small
	}


	string weapon_name
	{
		visiblewhen "*cvar:mn_displayweapon > 0"
		string		*cvar:mn_researchedlinkname
		pos			"285 704"
		size		"230 20"
		textalign	uc
		font		f_small
	}

	zone weapon_name_click
	{
		visiblewhen "*cvar:mn_displayweapon > 0"
		pos			"285 702"
		size		"230 20"
		onClick		{ cmd "mn_upgotoresearchedlink;" }
	}

	/** Firemodes */
	string header_firemode
	{
		visiblewhen "*cvar:mn_displayfiremode == 1"
		string		"_Firemode:"
		pos			"115 724"
		size		"150 20"
		textalign	cl
		font		f_small
	}

	pic firemode_dec
	{
		visiblewhen "*cvar:mn_changefiremode == 1"
		tooltip		"_Previous firemode"
		image		ui/buttons_small
		pos			"270 724"
		texl		"0 0"
		texh		"16 16"
		mousefx		true
		onClick		{ cmd "mn_decreasefiremode;" }
	}

	string firemode_name
	{
		visiblewhen "*cvar:mn_displayfiremode == 1"
		string		*cvar:mn_firemodename
		pos			"285 724"
		size		"230 20"
		textalign	cc
		font		f_small
	}

	pic firemode_inc
	{
		visiblewhen "*cvar:mn_changefiremode == 1"
		tooltip		"_Next firemode"
		image		ui/buttons_small
		pos			"510 724"
		texl		"16 0"
		texh		"32 16"
		mousefx		true
		onClick		{ cmd "mn_increasefiremode;" }
	}

	confunc update_ufopedia_layout {
		
		// is right column is empty?
		if ( ( ( *cvar:mn_upimage_top eq "" ) and ( *cvar:mn_upmodel_top eq "" ) ) and ( *cvar:mn_upmodel_bottom eq "" ) ) {
			*cvar:mn_expend = 1
		} else {
			*cvar:mn_expend = 0
		}

		// update tab
		if ( *cvar:mn_uppreavailable == 1 ) {
			cmd "mn_updateoption <path:root>.navigation preresearch display" 
		} else {
			cmd "mn_updateoption <path:root>.navigation preresearch hide"
			// update the tab if the hiden one is selected
			if ( *cvar:ufopedia_content eq "preresearch" ) {
				*cvar:ufopedia_content = "description"
			} 
		}

		if ( *cvar:ufopedia_content eq "description" ) {
			*ufopedia@invis = false
			*ufopedia_scroll@invis = false
			*description@invis = true
			*description_scroll@invis = true
			*techtree@invis = true
			*techtree_scroll@invis = true
			if ( *cvar:mn_expend == 1 ) {
				*ufopedia@width = 583
			} else {
				*ufopedia@width = 406
			}
			cmd "set mn_uppretext 0; mn_upupdate;"
		} elif ( *cvar:ufopedia_content eq "preresearch" ) {
			*ufopedia@invis = false
			*ufopedia_scroll@invis = false
			*description@invis = true
			*description_scroll@invis = true
			*techtree@invis = true
			*techtree_scroll@invis = true
			if ( *cvar:mn_expend == 1 ) {
				*ufopedia@width = 583
			} else {
				*ufopedia@width = 406
			}
			cmd "set mn_uppretext 1; mn_upupdate;"
		} elif ( *cvar:ufopedia_content eq "stats" ) {
			*ufopedia@invis = true
			*ufopedia_scroll@invis = true
			*description@invis = false
			*description_scroll@invis = false
			*techtree@invis = false
			*techtree_scroll@invis = false
		}
		
		cmd "del mn_expend"
	}

	confunc mn_upfbig
	{
	}

	confunc mn_upfsmall
	{
	}

	func onInit
	{
		*cvar:ufopedia_content = "description"
		cmd "mn_upcontent;"
		*node:root.summary.ufopedia_summary@viewpos = 0
		*node:root.summary.ufopedia_summary_scroll@current = 0
		*cvar:ufopedia_element = ""
		cmd "update_ufopedia_layout"
	}

	func onClose
	{
		cmd "mn_fireinit ufopedia;"
		cmd "del ufopedia_element"
	}
}
