// ==================
// MULTIPLAYER MENU
// ==================

window multiplayer
{
	confunc mn_push_multiplayer
	{
		cmd "game_setmode multiplayer; mn_push mp_serverbrowser;"
		*cvar:mn_multiplayer_tab 1
	}

	/** @brief prevent pushing this menu */
	func onInit
	{
		cmd "mn_pop"
		call *node:root.mn_push_multiplayer
	}
}

window abstract_multiplayer {
	// ==================
	// background
	// ==================

	pic background
	{
		image		ui/options_bg
	}

	// ==================
	// main navigation
	// ==================

	button button_team
	{
		string		"_Team"
		pos			"80 160"
		size		"256 60"
		image		ui/button_green_big
		font		f_menubig
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "mn_pop; mn_push mp_team;" }
	}

	button button_join
	{
		string		"_Join"
		pos			"80 225"
		size		"256 60"
		image		ui/button_green_big
		font		f_menubig
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "mn_pop; mn_push mp_serverbrowser;" }
	}

	button button_create
	{
		string		"_Create"
		pos			"80 290"
		size		"256 60"
		image		ui/button_green_big
		font		f_menubig
		color		"0 0.5 0 1"
		selectcolor "1 1 1 1"
		onClick		{ cmd "mn_pop; mn_push mp_create_server;" }
	}

	button button_lobby
	{
		string		"_Lobby"
		pos			"80 385"
		size		"256 60"
		image		ui/button_green_big
		font		f_menubig
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "irc_connect; mn_push irc;" }
	}


	button button_exit
	{
		string		"_Back"
		pos			"80 480"
		size		"256 60"
		image		ui/button_green_big
		font		f_menubig
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "mn_pop;" }
	}

}

window abstract_multiplayer_join extends abstract_multiplayer {
	// ==================
	// main navigation
	// ==================

	tab join_navigation {
		pos		"462 73"
		size	"486 40"
		cvar	*cvar:mn_multiplayer_tab
		option internet {
			label	"_Internet/Lan"
			value	"1"
		}
/*		option lan {
			label	"_Lan"
			value	"2"
		}
*/
		option bookmark {
			label	"_Bookmark"
			value	"3"
		}
/*
		option bookmark {
			label	"_Lobby"
			value	"4"
		}
*/
		onChange {
			if ( *cvar:mn_multiplayer_tab == 1 ) {
				cmd "mn_pop;mn_push mp_serverbrowser;"
			} elif ( *cvar:mn_multiplayer_tab == 2 ) {
	
			} elif ( *cvar:mn_multiplayer_tab == 3 ) {
				cmd "mn_pop;mn_push mp_bookmarks;"
			} elif ( *cvar:mn_multiplayer_tab == 4 ) {
				cmd "irc_connect; mn_push irc;"
			}
		}
	}
}

// ==================
// SERVER INFO
// ==================
window serverinfo
{
	pic background
	{
		image		ui/multiplayer_bg
	}

	text server_info
	{
		dataid		TEXT_STANDARD
		pos			"500 200"
		size		"420 490"
		lineheight	20
		tabwidth	200
		rows		"24"
	}

	string user_list_str
	{
		string		"_Userlist"
		pos			"50 115"
		size		"300 20"
		color		"1 1 1 1"
		font		"f_normal_bold"
		textalign	cc
	}
	string user_list_head1
	{
		string		"_Name"
		pos			"50 135"
		size		"150 20"
		color		"1 1 1 1"
		font		"f_verysmall"
		textalign	ul
	}
	string user_list_head2
	{
		string		"_Team"
		pos			"200 135"
		size		"150 20"
		color		"1 1 1 1"
		font		"f_verysmall"
		textalign	ul
	}
	text user_list
	{
		dataid	TEXT_LIST
		pos			"50 170"
		size		"300 380"
		lineheight	20
		tabwidth	200
		font		"f_verysmall"
		mousefx		true
		border		3
		bordercolor	"0 0.5 0 1"
		padding		10
		bgcolor		"0.3 0.3 0.3 0.4"
		rows		"19"
	}

	string mapstring
	{
		string		*cvar:mn_svmapname
		font		f_menubig
		color		"1 1 1 1"
		pos			"500 110"
		size		"300 35"
	}

	pic mappic
	{
		image		*cvar:mn_mappic
		pos			"800 80"
		texl		"0 0"
		texh		"128 96"
	}

	button button_connect
	{
		string		"_Connect"
		pos			"70 580"
		size		"256 64"
		image		ui/button_green_big
		font		f_menubig
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "connect *cvar:mn_server_ip;" }
	}

	custombutton button_exit
	{
		tooltip		"_Leave Serverbrowser"
		image		ui/button_background
		texl		"192 0"
		pos			"26 720"
		size		"64 37"
		icon		return
		onClick		{ cmd "mn_pop; pingservers;" }
	}

	button button_favorite
	{
		string		"_Bookmark"
		pos			"200 720"
		size		"128 35"
		image		ui/button_blue
		font		f_menu
		color		"1 1 1 0.8"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "bookmark_add;" }
	}

	func onInit
	{
	}

	func onClose
	{
		cmd	"pingservers; check_bookmarks;"
	}
}

// ==================
// JOIN LAN MENU
// ==================
window mp_serverbrowser extends abstract_multiplayer_join
{
	{
		timeout	60000
	}

	panel content {
		{
			pos			"415 120"
			size		"608 768"
		}

		string status_str
		{
			string		"_Serverfilter:"
			pos			"150 7"
			size		"150 20"
		}
		string status_value
		{
			string		*cvar:mn_serverliststr
			pos			"300 7"
			size		"150 20"
			textalign	uc
		}
		custombutton status_dec
		{
			pos			"455 7"
			size		"16 16"
			icon		prev
			onClick		{
				cmd "mn_modifywrap cl_serverlist -1 0 2;"
				cmd "mn_translate cl_serverlist mn_serverliststr 0:All,1:Free,2:Playing;"
				cmd "pingservers refresh;"
			}
		}
		custombutton status_inc
		{
			pos			"470 7"
			size		"16 16"
			icon		next
			onClick		{
				cmd "mn_modifywrap cl_serverlist +1 0 2;"
				cmd "mn_translate cl_serverlist mn_serverliststr 0:All,1:Free,2:Playing;"
				cmd "pingservers refresh;"
			}
		}

		// ====================
		// lan / inet buttons
		// ====================

		// servers_click is CL_ServerListClick_f in cl_main.c
		/* textlist using tabbed text */
		text servers
		{
			pos			"65 40"
			width		"430"
			lineheight	20
			tabwidth	48
			font		"f_verysmall"
			mousefx		true
			border		3
			bordercolor	"0 0.5 0 1"
			padding		10
			bgcolor		"0.3 0.3 0.3 0.4"
			rows		"22"
			dataid		TEXT_LIST
			onClick		{ cmd "servers_click <lineselected>" }
			onRClick	{ cmd "pingservers;" }
			onViewChange {
				*(abstractscrollbar)content.servers_scroll@fullsize = <fullsize>
				*(abstractscrollbar)content.servers_scroll@current = <viewpos>
				*(abstractscrollbar)content.servers_scroll@viewsize = <viewsize>
			}
		}

		vscrollbar servers_scroll {
			image		ui/scrollbar_v_green
			pos			"500 40"
			height		"440"
			current		0
			viewsize	22
			fullsize	22
			onChange	{ *node:parent.servers@viewpos = <current> }
		}

		button button_serverbrowser_refresh
		{
			string		"_Refresh"
			image		ui/button_green_small
			color		"0 0.8 0 1"
			font		f_small
			pos			"335 490"
			size		"176 32"
			onClick		{ cmd "pingservers refresh;" }
		}

		// ==================
		// nickname
		// ==================
/*
		string current_name
		{
			string		"_Name:"
			pos			"65 455"
			size		"256 20"
		}

		textentry slot_name
		{
			string		*cvar:cl_name
			image		ui/button_green_verysmall
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	cl
			padding		8
			font		"f_verysmall"
			pos			"65 475"
			size		"256 34"
		}
*/

		// ===================
		// connect_to_ip
		// ===================
		string current_inet_connect_to_ip
		{
			string		"_Connect to IP"
			pos			"65 520"
			size		"256 20"
		}

		textentry slot_ip
		{
			string		*cvar:mn_server_ip
			image		ui/button_green_verysmall
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	cl
			padding		8
			font		"f_verysmall"
			pos			"65 540"
			size		"256 34"
		}
		button button_inet_connect_to_ip
		{
			string		"_Connect"
			image		ui/button_green_small
			color		"0 0.8 0 1"
			font		f_small
			pos			"335 540"
			size		"176 32"
			onClick		{ cmd "server_info *cvar:mn_server_ip;" }
		}
	}

	// ====================
	// init
	// ====================
	func onInit
	{
		cmd "set cl_serverlist 0;"
		cmd "pingservers refresh;"
		cmd "mn_translate cl_serverlist mn_serverliststr 0:All,1:Free,2:Playing;"
	}

	func onEvent
	{
		cmd "pingservers refresh;"
	}
}

// ==================
// INTERNET MENU
// ==================

window mp_bookmarks extends abstract_multiplayer_join
{
	panel content {
		{
			pos			"470 120"
			size		"470 580"
		}

		// ====================
		// lan / inet buttons
		// ====================

		string inet_bookmarks
		{
			string		"_Bookmarks"
			font		f_menubig
			color		"0 0.5 0 1"
			pos			"15 30"
			size		"440 35"
			textalign	uc
		}

		rows lines {
			pos			"90 95"
			size		"310 480"
			color1		"0 0.08 0 1"
			color2		"0 0.16 0 1"
			lineheight	30
		}

		string adr0 { pos "95 100" string *cvar:adr0 size "300 20" onClick { cmd "server_info *cvar:adr0;" } tooltip "_Connect to this server" }
		custombutton resetadr0 { pos "65 99" size "22 22" icon garbage	onClick { cmd "del adr0; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr1 { pos "95 130" string *cvar:adr1 size "300 20" onClick { cmd "server_info *cvar:adr1;" } tooltip "_Connect to this server" }
		custombutton resetadr1 { pos "65 129" size "22 22" icon garbage	onClick { cmd "del adr1; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr2 { pos "95 160" string *cvar:adr2 size "300 20" onClick { cmd "server_info *cvar:adr2;" } tooltip "_Connect to this server" }
		custombutton resetadr2 { pos "65 159" size "22 22" icon garbage	onClick { cmd "del adr2; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr3 { pos "95 190" string *cvar:adr3 size "300 20" onClick { cmd "server_info *cvar:adr3;" } tooltip "_Connect to this server" }
		custombutton resetadr3 { pos "65 189" size "22 22" icon garbage	onClick { cmd "del adr3; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr4 { pos "95 220" string *cvar:adr4 size "300 20" onClick { cmd "server_info *cvar:adr4;" } tooltip "_Connect to this server" }
		custombutton resetadr4 { pos "65 219" size "22 22" icon garbage	onClick { cmd "del adr4; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr5 { pos "95 250" string *cvar:adr5 size "300 20" onClick { cmd "server_info *cvar:adr5;" } tooltip "_Connect to this server" }
		custombutton resetadr5 { pos "65 249" size "22 22" icon garbage	onClick { cmd "del adr5; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr6 { pos "95 280" string *cvar:adr6 size "300 20" onClick { cmd "server_info *cvar:adr6;" } tooltip "_Connect to this server" }
		custombutton resetadr6 { pos "65 279" size "22 22" icon garbage	onClick { cmd "del adr6; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr7 { pos "95 310" string *cvar:adr7 size "300 20" onClick { cmd "server_info *cvar:adr7;" } tooltip "_Connect to this server" }
		custombutton resetadr7 { pos "65 309" size "22 22" icon garbage	onClick { cmd "del adr7; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr8 { pos "95 340" string *cvar:adr8 size "300 20" onClick { cmd "server_info *cvar:adr8;" } tooltip "_Connect to this server" }
		custombutton resetadr8 { pos "65 339" size "22 22" icon garbage	onClick { cmd "del adr8; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr9 { pos "95 370" string *cvar:adr9 size "300 20" onClick { cmd "server_info *cvar:adr9;" } tooltip "_Connect to this server" }
		custombutton resetadr9 { pos "65 369" size "22 22" icon garbage	onClick { cmd "del adr9; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr10 { pos "95 400" string *cvar:adr10 size "300 20" onClick { cmd "server_info *cvar:adr10;" } tooltip "_Connect to this server" }
		custombutton resetadr10 { pos "65 399" size "22 22" icon garbage	onClick { cmd "del adr10; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr11 { pos "95 430" string *cvar:adr11 size "300 20" onClick { cmd "server_info *cvar:adr11;" } tooltip "_Connect to this server" }
		custombutton resetadr11 { pos "65 429" size "22 22" icon garbage	onClick { cmd "del adr11; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr12 { pos "95 460" string *cvar:adr12 size "300 20" onClick { cmd "server_info *cvar:adr12;" } tooltip "_Connect to this server" }
		custombutton resetadr12 { pos "65 459" size "22 22" icon garbage	onClick { cmd "del adr12; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr13 { pos "95 490" string *cvar:adr13 size "300 20" onClick { cmd "server_info *cvar:adr13;" } tooltip "_Connect to this server" }
		custombutton resetadr13 { pos "65 489" size "22 22" icon garbage	onClick { cmd "del adr13; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr14 { pos "95 520" string *cvar:adr14 size "300 20" onClick { cmd "server_info *cvar:adr14;" } tooltip "_Connect to this server" }
		custombutton resetadr14 { pos "65 519" size "22 22" icon garbage	onClick { cmd "del adr14; check_bookmarks;" } tooltip "_Delete this bookmark" }

		string adr15 { pos "95 550" string *cvar:adr15 size "300 20" onClick { cmd "server_info *cvar:adr15;" } tooltip "_Connect to this server" }
		custombutton resetadr15 { pos "65 549" size "22 22" icon garbage	onClick { cmd "del adr15; check_bookmarks;" } tooltip "_Delete this bookmark" }
	}

	confunc check_bookmarks {
		*cvar:i = 0
		while ( *cvar:i <= 15 ) {
			if ( *cvar:adr<cvar:i> ne "" ) {
				*content.resetadr<cvar:i>@invis = false
				*content.adr<cvar:i>@invis = false
			} else {
				*content.resetadr<cvar:i>@invis = true
				*content.adr<cvar:i>@invis = true
			}
			*cvar:i = ( *cvar:i + 1 )
		}
		delete *cvar:i
	}

	func onInit {
		cmd "check_bookmarks;"
	}
}

// ==================
// SERVER MENU
// ==================

window mp_create_server extends abstract_multiplayer
{

	tab create_natigation {
		pos		"472 200"
		size	"466 40"
		cvar	*cvar:mn_server_config_tab
		option map {
			label	"_Map"
			value	"1"
		}
		option option {
			label	"_Options"
			value	"2"
		}
		option security {
			label	"_Security/registration"
			value	"3"
		}
		onChange {
			cmd "mp_update_config_navigation;"
		}
	}

	panel panel_main {
		{
			pos			"415 80"
			size		"608 100"
		}

		// ===================
		// HOSTNAME
		// ===================
		string current_hostname
		{
			string		"_Hostname:"
			pos			"85 5"
			size		"256 20"
		}
		textentry slot_hostname
		{
			string		*cvar:sv_hostname
			image		ui/button_green_verysmall
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	cl
			padding		8
			font		"f_verysmall"
			pos			"245 0"
			size		"256 34"
		}

		// ===================
		// GAME TYPE
		// ===================
		todo todo1 {
			string	"use a selectbox for the game type"
			pos		"500 40"
		}

		string gametype_str
		{
			string		"_Gametype:"
			pos			"85 40"
			size		"280 20"
		}
		string gametype_value
		{
			string		*cvar:sv_gametype
			pos			"380 40"
			size		"80 20"
			textalign	uc
			onWheelUp	{ cmd "mp_prevgametype;" }
			onWheelDown	{ cmd "mp_nextgametype;" }
		}
		custombutton gametype_dec
		{
			pos			"364 40"
			size		"16 16"
			icon		prev
			onClick		{ cmd "mp_prevgametype;" }
			onWheelUp	{ cmd "mp_prevgametype;" }
			onWheelDown	{ cmd "mp_nextgametype;" }
		}
		custombutton gametype_inc
		{
			pos			"460 40"
			size		"16 16"
			icon		next
			onClick		{ cmd "mp_nextgametype;" }
			onWheelUp	{ cmd "mp_prevgametype;" }
			onWheelDown	{ cmd "mp_nextgametype;" }
		}

		// ===================
		// TEAMPLAY
		// ===================
		string server_teamplay_str
		{
			string		"_Teamplay:"
			pos			"85 70"
			size		"280 20"
		}
		checkbox server_teamplay_value
		{
			current		*cvar:sv_teamplay
			pos			"410 70"
			size		"20 18"
			image		"ui/checkbox_blue"
		}

	}

	panel panel_map {
		{
			pos			"415 250"
			size		"608 768"
		}

		// ===================
		// PREVIEW
		// ===================

		string preview_label
		{
			string		"_Preview:"
			pos			"85 0"
			size		"256 20"
		}

		pic mappic
		{
			image		*cvar:mn_mappic
			pos			"85 30"
			texl		"0 0"
			texh		"128 96"
			onWheelUp	{ cmd "mn_prevmap;" }
			onWheelDown	{ cmd "mn_nextmap;" }
			onClick		{ cmd "mn_nextmap;" }
			onRClick		{ cmd "mn_prevmap;" }
		}
		pic mappic2
		{
			image		*cvar:mn_mappic2
			pos			"225 30"
			texl		"0 0"
			texh		"128 96"
			onWheelUp	{ cmd "mn_prevmap;" }
			onWheelDown	{ cmd "mn_nextmap;" }
			onClick		{ cmd "mn_nextmap;" }
			onRClick		{ cmd "mn_prevmap;" }
		}
		pic mappic3
		{
			image		*cvar:mn_mappic3
			pos			"365 30"
			texl		"0 0"
			texh		"128 96"
			onWheelUp	{ cmd "mn_prevmap;" }
			onWheelDown	{ cmd "mn_nextmap;" }
			onClick		{ cmd "mn_nextmap;" }
			onRClick		{ cmd "mn_prevmap;" }
		}

		// ===================
		// MAP
		// ===================
		string map_label
		{
			string		"_Map name:"
			pos			"85 155"
			size		"256 20"
		}
		textentry slot_map
		{
			string		*cvar:mn_svmapname
			image		ui/button_green_verysmall
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	cl
			padding		8
			font		"f_verysmall"
			pos			"245 150"
			size		"256 34"
			disabled	true
			onWheelUp	{ cmd "mn_prevmap;" }
			onWheelDown	{ cmd "mn_nextmap;" }
		}

		custombutton prev_map
		{
			pos			"223 155"
			size		"16 16"
			icon		prev
			onWheelUp	{ cmd "mn_prevmap;" }
			onWheelDown	{ cmd "mn_nextmap;" }
			onClick		{ cmd "mn_prevmap;" }
		}
		custombutton next_map
		{
			pos			"505 155"
			size		"16 16"
			icon		next
			onWheelUp	{ cmd "mn_prevmap;" }
			onWheelDown	{ cmd "mn_nextmap;" }
			onClick		{ cmd "mn_nextmap;" }
		}

		// ===================
		// GAMETYPE
		// ===================

		string allowedtype_label
		{
			string		"_Allowed gametype:"
			pos			"85 200"
			size		"256 20"
		}

		string string_gametypes
		{
			string		*cvar:mn_mapgametypes
			pos			"245 200"
			size		"256 32"
			font		"f_verysmall"
			longlines	wrap
		}

		// ===================
		// DAY + NIGHT
		// ===================
		todo todo3 {
			pos "500 245"
			string "It should be better to have a selectbox with day/night"
		}
		string daynight_str
		{
			string		"_Day version:"
			pos			"85 245"
			size		"280 20"
		}
		checkbox daynight_value
		{
			current		*cvar:mn_serverday
			pos			"410 245"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
	}

	panel panel_option {
		{
			pos			"475 260"
			size		"460 350"
		}

		// ===================
		// MORALE
		// ===================
		string morale_str
		{
			string		"_Enable morale states:"
			pos			"25 0"
			size		"300 20"
		}
		checkbox morale_value
		{
			current		*cvar:sv_enablemorale
			pos			"350 1"
			size		"20 18"
			image		"ui/checkbox_blue"
		}

		// ===================
		// MAXCLIENTS
		// ===================
		string maxclients_str
		{
			string		"_Max. clients:"
			tooltip		"_Sets the max allowed clients to connect"
			pos			"25 40"
			size		"300 20"
		}
		string maxclients_value
		{
			string		*cvar:sv_maxclients
			pos			"335 40"
			size		"50 20"
			textalign	uc
		}
		spinner maxclients_change
		{
			current		*cvar:sv_maxclients
			image		ui/spinner_blue
			pos			"390 40"
			min			2
			max			16
			delta		1
		}

		// ===================
		// G_MAXPERPLAYER
		// ===================
		string maxperplayer_str
		{
			string		"_Max. soldiers per team:"
			tooltip		"_Sets the max allowed soldiers per team"
			pos			"25 60"
			size		"300 20"
		}
		string maxperplayer_value
		{
			string		*cvar:sv_maxsoldiersperteam
			pos			"335 60"
			size		"50 20"
			textalign	uc
		}
		spinner maxperplayer_change
		{
			current		*cvar:sv_maxsoldiersperteam
			image		ui/spinner_blue
			pos			"390 60"
			min			1
			max			8
			delta		1
		}

		// =====================
		// MAXSOLDIER PER PLAYER
		// =====================
		string maxsoldierperplayer_str
		{
			string		"_Max. soldiers per player:"
			tooltip		"_Sets the max allowed soldiers per player when in teamplay"
			pos			"25 80"
			size		"300 20"
		}
		string maxsoldierperplayer_value
		{
			string		*cvar:sv_maxsoldiersperplayer
			pos			"335 80"
			size		"50 20"
			textalign	uc
		}
		spinner maxsoldierperplayer_change
		{
			current		*cvar:sv_maxsoldiersperplayer
			image		ui/spinner_blue
			pos			"390 80"
			min			1
			max			8
			delta		1
		}

		// ===================
		// MAX PLAYER PER TEAM
		// max is half of sv_maxclients
		// ===================
		string server_maxplayerperteam_str
		{
			string		"_Max. human players per team:"
			pos			"25 100"
			size		"300 20"
		}
		string server_maxplayerperteam_value
		{
			string		*cvar:sv_maxplayersperteam
			pos			"335 100"
			size		"50 20"
			textalign	uc
		}
		spinner maxplayersperteam_change
		{
			current		*cvar:sv_maxplayersperteam
			image		ui/spinner_blue
			pos			"390 100"
			min			1
			max			8
			delta		1
		}

		// ===================
		// AI_NUMACTORS
		// ===================
		string ai_numactors_str
		{
			string		"_Max. AI actors:"
			tooltip		"_Max AI actors for teamplay mode"
			pos			"25 120"
			size		"300 20"
		}
		string ai_numactors_value
		{
			string		*cvar:ai_numactors
			pos			"335 120"
			size		"50 20"
			textalign	uc
		}
		spinner ai_numactors_change
		{
			current		*cvar:ai_numactors
			image		ui/spinner_blue
			pos			"390 120"
			min			0
			max			8
			delta		1
		}
	}

	panel panel_security {
		{
			pos			"415 250"
			size		"608 768"
		}

		// =====================
		// PUBLIC - MASTERSERVER
		// =====================
		string public_str
		{
			string		"_Make this server public:"
			pos			"85 20"
			size		"280 20"
		}
		checkbox public_value
		{
			current		*cvar:sv_public
			pos			"410 20"
			size		"20 18"
			image		"ui/checkbox_blue"
		}

		// ===================
		// SERVER_PASSWORD
		// ===================
		string current_server_pwd
		{
			string		"_Server-Password:"
			pos			"85 80"
			size		"256 20"
		}
		textentry slot_server_pwd
		{
			string		*cvar:password
			image		ui/button_green_verysmall
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	cl
			padding		8
			font		"f_verysmall"
			pos			"245 75"
			size		"256 34"
			ispassword	true
		}

		// ===================
		// RCON_PASSWORD
		// ===================
		string current_rcon_pwd
		{
			string		"_RCON-Password:"
			pos			"85 140"
			size		"256 20"
		}
		textentry slot_rcon_pwd
		{
			string		*cvar:rcon_password
			image		ui/button_green_verysmall
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	cl
			padding		8
			font		"f_verysmall"
			pos			"245 135"
			size		"256 34"
			ispassword	true
		}

	}

	// ===================
	// FIREWALL TEST
	// ===================

	/** @todo fix it, it can't support string property */
	text firewall_str
	{
		string		"_Make sure that you've opened the TCP port 27910 in your firewall settings"
		pos			"500 530"
		size		"400 100"
		longlines	wrap
		textalign	uc
		font		"f_small"
		dataid		TEXT_STANDARD
	}

	// ====================
	// create_server button
	// ====================

	button button_server
	{
		string		"_Start server"
		pos			"570 630"
		size		"256 64"
		image		ui/button_green_big
		font		f_menubig
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "mp_startserver;" }
	}

	// ===================
	// Interaction
	// ===================

	confunc mp_update_config_navigation {
		*panel_map@invis true
		*panel_option@invis true
		*panel_security@invis true
		if ( *cvar:mn_server_config_tab == 1 ) {
			*panel_map@invis false
		}
		elif ( *cvar:mn_server_config_tab == 2 ) {
			*panel_option@invis false
		}
		elif ( *cvar:mn_server_config_tab == 3 ) {
			*panel_security@invis false
		}
	}

	func onInit
	{
		cmd "killserver;set sv_maxplayersperteam 8;set sv_roundtimelimit 0;set mn_serverday 1;"
		cmd "mp_updategametype;mn_getmaps;"
		*cvar:mn_server_config_tab 1
		cmd "mp_update_config_navigation;"
		cmd "mn_datareset TEXT_STANDARD;"
	}
}

// ===========================================
// MULTIPLAYER TEAM SELECTION AND WAIT DIALOG
// ===========================================
window multiplayer_wait
{
	{
		timeout 	2000
		image		ui/world_bg
		pos			"0 0"
		size		"1024 768"
	}

	panel partyinfo {
		{
			pos		"100 30"
			size	"400 430"
			padding	5
			image	"ui/panel_window"
		}

		string title
		{
			string		"_Party info"
			font		f_big
			color		"1 1 1 1"
			pos			"0 0"
			size		"415 50"
			textalign	cc
		}

		string mapname
		{
			font		f_small
			color		"1 1 1 1"
			string		*cvar:mn_svmapname
			pos			"50 50"
			size		"300 20"
			textalign	"cc"
		}

		pic mappic
		{
			pos			"40 80"
			size		"320 200"
			image		*cvar:mn_mappicbig
		}

		string maxplayersperteams_str
		{
			string		"_Max. players per team"
			pos			"50 310"
			size		"240 20"
		}

		string maxplayersperteams_value
		{
			string		*cvar:mn_maxplayersperteam
			pos			"300 310"
			size		"50 20"
		}

		string maxteams_str
		{
			string		"_Max. teams for this map"
			pos			"50 330"
			size		"240 20"
		}

		string maxteams_value
		{
			string		*cvar:mn_maxteams
			pos			"300 330"
			size		"50 20"
		}
	}

	panel players {
		{
			pos		"520 30"
			size	"400 250"
			padding	5
			image	"ui/panel_window"
		}

		string title
		{
			string		"_Players connected"
			font		f_big
			color		"1 1 1 1"
			pos			"0 0"
			size		"415 50"
			textalign	cc
		}

		panel ready {
			{
				layout LAYOUT_TOP_DOWN_FLOW
				layoutMargin 2
				padding		0
				pos			"50 50"
				size		"16 300"
			}

			pic p0 { size "16 16" }
			pic p1 { size "16 16" }
			pic p2 { size "16 16" }
			pic p3 { size "16 16" }
			pic p4 { size "16 16" }
			pic p5 { size "16 16" }
			pic p6 { size "16 16" }
			pic p7 { size "16 16" }
			pic p8 { size "16 16" }
			pic p9 { size "16 16" }
		}

		text userlist
		{
			dataid		TEXT_MULTIPLAYER_USERLIST
			pos			"80 50"
			size		"150 300"
		}

		text teamlist
		{
			dataid		TEXT_MULTIPLAYER_USERTEAM
			pos			"200 50"
			size		"150 300"
		}
	}

	panel teamselection {
		{
			pos		"520 310"
			size	"400 150"
			padding	5
			image	"ui/panel_window"
		}

		string title
		{
			string		"_Team selection"
			font		f_big
			color		"1 1 1 1"
			pos			"0 0"
			size		"415 50"
			textalign	cc
		}

		text teamnum_status
		{
			dataid		TEXT_STANDARD
			pos			"0 50"
			size		"400 16"
			lineheight	16
			tabwidth	60
			rows		1
			textalign	cc
		}

		string teamnum_str
		{
			string		"_Team preference:"
			pos			"25 80"
			size		"185 20"
		}
		string teamnum_value
		{
			string		*cvar:cl_teamnum
			pos			"210 80"
			size		"40 20"
			textalign	ur
		}
		pic teamnum_inc
		{
			visiblewhen	"*cvar:cl_ready != 1"
			tooltip		"_Next free team"
			image		ui/buttons_small
			pos			"268 82"
			texl		"0 0"
			texh		"16 16"
			mousefx		true
			onClick		{ cmd "teamnum_inc;" }
		}
		pic teamnum_dec
		{
			visiblewhen	"*cvar:cl_ready != 1"
			tooltip		"_Previous free team"
			image		ui/buttons_small
			pos			"284 82"
			texl		"16 0"
			texh		"32 16"
			mousefx		true
			onClick		{ cmd "teamnum_dec;" }
		}

		/** @todo only for admin, and command it self should check the user */
		button button_force
		{
			visiblewhen "*cvar:cl_admin == 1"
			string		"_Force start"
			tooltip		"_Start the game even if not all teams is ready or have joined"
			image		ui/button_blue
			pos			"50 110"
			size		"120 30"
			color		"0.8 0.8 0.8 1"
			selectcolor "1 1 1 1"
			onClick		{ cmd "sv startgame;" }
		}

		checkbox cb_ready
		{
			tooltip		"_Finish the teamselection and start the game"
			image		ui/checkbox_green
			pos			"200 120"
			size		"20 20"
			current		*cvar:cl_ready
		}
		string cb_ready_label
		{
			string		"_Are you ready?"
			tooltip		"_Finish the teamselection and start the game"
			pos			"230 120"
			size		"180 20"
			onClick		{ call *node:parent.cb_ready@toggle }
		}
	}

	todo adminteam { pos "30 310" string "Feature: admin can change a player's team" }
	todo adminstart { pos "30 340" string "Feature: admin can force a start of the party" }
	todo fixchat { pos "30 370" string "Fix irc/chat usability (last line on bottom and visible)" }
	todo inviteothers { pos "30 400" string "Invite button only in inet mode, not in lan mode"}

	button swap_irc_connection_status {
		string		"_Activate IRC"
		image		ui/button_blue
		pos			"30 550"
		size		"150 40"
		color		"0.8 0.8 0.8 1"
		selectcolor "1 1 1 1"
		onClick {
			if ( *cvar:mn_ircconnected == 1 ) {
				cmd "irc_disconnect;"
				*cvar:mn_ircconnected = 0
				*node:this@string = "_Activate IRC"
				*node:parent.swap_irc_users_visibility@disabled = true
				*node:parent.invite_players@disabled = true
			} else {
				cmd "irc_connect;"
				*cvar:mn_ircconnected = 1
				*node:this@string = "_Deactivate IRC"
				*node:parent.swap_irc_users_visibility@disabled = false
				*node:parent.invite_players@disabled = false
			}
		}
	}

	button swap_irc_users_visibility {
		string		"_Display IRC users"
		image		ui/button_blue
		pos			"30 600"
		size		"150 40"
		disabled	true
		color		"0.8 0.8 0.8 1"
		selectcolor "1 1 1 1"
		onClick {
			if ( *node:parent.allchats.ircscreen.users@invis == true ) {
				*node:parent.allchats.ircscreen.users@invis = false
				*node:this@string = "_Display IRC users"
			} else {
				*node:parent.allchats.ircscreen.users@invis = true
				*node:this@string = "_Hide IRC users"
			}
		}
	}

	button invite_players {
		string		"_Invite others"
		image		ui/button_blue
		tooltip		"_Invite players that are online in the lobby"
		pos			"30 650"
		size		"150 40"
		disabled	true
		color		"0.8 0.8 0.8 1"
		selectcolor "1 1 1 1"
		onClick {
			cmd "irc_invite;"
		}
	}

	panel allchats {
		{
			pos				"200 550"
			size			"800 200"
			bordercolor		"0.5 0.5 0.5 1"
			border			2
			padding			5
			layout			LAYOUT_BORDER
		}

		tab selectchat {
			align	LAYOUTALIGN_TOP
			height	40
			cvar	*cvar:mn_chatselect
			onChange {
				if ( *cvar:mn_chatselect eq "irc" ) {
					*node:parent.ircscreen@invis = false
					*node:parent.chatscreen@invis = true
				}
				if ( *cvar:mn_chatselect eq "chat" ) {
					*node:parent.ircscreen@invis = true
					*node:parent.chatscreen@invis = false
				}
			}

			option chat {
				label	"_Party chat"
				value	"chat"
			}
			option irc {
				label	"_Global lobby (IRC)"
				value	"irc"
			}
		}

		panel ircscreen {
			{
				layout			LAYOUT_BORDER
				align			LAYOUTALIGN_MIDDLE
				layoutMargin	5
				padding			5
				bordercolor		"0.5 0.5 0.5 1"
				border			2
				bgcolor			"0 0.1 0 0.7"
				invis			true
			}

			panel users {
				{
					width			"200"
					layout			LAYOUT_BORDER
					align			LAYOUTALIGN_RIGHT
					layoutMargin	5
					padding			0
					invis			true
				}

				textlist list
				{
					lineheight	22
					dataid		TEXT_IRCUSERS
					rows		"27"
					onClick		{ cmd "irc_userlist_click <lineselected>" }
					onRClick	{ cmd "irc_userlist_rclick <lineselected>" }
					onViewChange {
						*node:(abstractscrollbar)parent.scroll@fullsize <fullsize>
						*node:(abstractscrollbar)parent.scroll@current <viewpos>
						*node:(abstractscrollbar)parent.scroll@viewsize <viewsize>
					}
				}

				vscrollbar scroll {
					image		ui/scrollbar_v_green
					align		LAYOUTALIGN_RIGHT
					onChange 	{
						*node:parent.list@viewpos = <current>
					}
				}
			}

			text irc
			{
				dataid		TEXT_IRCCONTENT
				align		LAYOUTALIGN_MIDDLE
				font		"f_verysmall"
				rows		"10"
				lineheight	"16"
				padding		5
				onViewChange {
					*node:(abstractscrollbar)parent.irc_scroll@fullsize = <fullsize>
					*node:(abstractscrollbar)parent.irc_scroll@viewsize = <viewsize>
					*node:(abstractscrollbar)parent.irc_scroll@current = <viewpos>
				}
			}

			vscrollbar irc_scroll {
				image		ui/scrollbar_v_green
				align		LAYOUTALIGN_RIGHT
				onChange 	{
					*node:parent.irc@viewpos = <current>
				}
			}
	
			panel input {
				{
					layout			LAYOUT_PACK
					layoutMargin	5
					align			LAYOUTALIGN_BOTTOM
					height			25
				}
				
				string say_label {
					align		LAYOUTALIGN_LEFT
					width		"50"
					textalign	cl
					string		"_Say:"
				}
				
				textentry say {
					align		LAYOUTALIGN_FILL
					image		ui/button_green_verysmall
					string		*cvar:irc_send_buffer
					color 		"0 .78 0 1"
					selectcolor	"1 1 1 1"
					textalign	cl
					padding		8
					font		"f_verysmall"
					clickoutabort	true
					onChange	{
						cmd "irc_chanmsg;"
						cmd "multiplayer_ircsay_edit;"
					}
				}
				
				/** @todo move it into say@onChange
				 * But at the moment this call into that callback dont work
				 * without an intermediat command
				 */
				confunc multiplayer_ircsay_edit {
					call *node:parent.say@edit
				}
			}
		}

		panel chatscreen {
			{
				layout			LAYOUT_BORDER
				align			LAYOUTALIGN_MIDDLE
				layoutMargin	5
				padding			5
				bordercolor		"0.5 0.5 0.5 1"
				border			2
				bgcolor			"0 0.1 0 0.7"
			}
		
			text chat
			{
				dataid		TEXT_CHAT_WINDOW
				align		LAYOUTALIGN_MIDDLE
				font		"f_verysmall"
				rows		"10"
				lineheight	"16"
				padding		5
				onViewChange {
					*node:(abstractscrollbar)parent.chat_scroll@fullsize = <fullsize>
					*node:(abstractscrollbar)parent.chat_scroll@viewsize = <viewsize>
					*node:(abstractscrollbar)parent.chat_scroll@current = <viewpos>
				}
			}
	
			vscrollbar chat_scroll {
				image		ui/scrollbar_v_green
				align		LAYOUTALIGN_RIGHT
				onChange 	{
					*node:parent.chat@viewpos = <current>
				}
			}
	
			panel input {
				{
					layout			LAYOUT_PACK
					layoutMargin	5
					align			LAYOUTALIGN_BOTTOM
					height			25
				}
				
				string say_label {
					align		LAYOUTALIGN_LEFT
					width		"50"
					textalign	cl
					string		"_Say:"
				}
				
				textentry say {
					align		LAYOUTALIGN_FILL
					image		ui/button_green_verysmall
					string		*cvar:mn_entry
					color 		"0 .78 0 1"
					selectcolor	"1 1 1 1"
					textalign	cl
					padding		8
					font		"f_verysmall"
					clickoutabort	true
					onChange	{
						cmd "say <cvar:mn_entry>"
						*cvar:mn_entry = ""
						cmd "multiplayer_chatsay_edit;"
					}
				}
				
				/** @todo move it into say@onChange
				 * But at the moment this call into that callback dont work
				 * without an intermediat command
				 */
				confunc multiplayer_chatsay_edit {
					call *node:parent.say@edit
				}
			}
		}
	}

	/**
	 * @param <1> player ID/line
	 * @param <2> 0: not ready, 1: ready
	 * @todo use an image instead of a color
	 */
	confunc multiplayer_playerIsReady {
		if ( ( <1> < 10 ) and ( <1> >= 0 ) ) {
			if ( <2> == 0 ) {
				*node:root.players.ready.p<1>@image = "icons/off"
				*node:root.players.ready.p<1>@invis = false
			} else {
				*node:root.players.ready.p<1>@image = "icons/on"
				*node:root.players.ready.p<1>@invis = false
			}
		}
	}

	/**
	 * @param <1> number of players
	 */
	confunc multiplayer_playerNumber {
		*cvar:tmp = <1>
		while ( *cvar:tmp < 10 ) {
			*node:root.players.ready.p<1>@invis = true
			*cvar:tmp = ( *cvar:tmp + 1 )
		}
		delete *cvar:tmp
	}

	/**
	 * @param <1> boolean whether this is a teamplay match
	 */
	confunc multiplayer_setTeamplay {
		// @todo - deactivate team change if the given boolean is false	(0)
	}

	// ===================
	// EVENTS
	// ===================

	// update every 2 sec
	func onEvent
	{
		cmd "mp_selectteam_init;"
	}

	func onInit
	{
		cmd "mp_selectteam_init;"
		*cvar:mn_entry = ""
		*cvar:mn_chatselect = "chat"
		*cvar:cl_ready = 0
	}
	
	func onClose
	{
		delete *cvar:mn_entry	
		delete *cvar:mn_chatselect
	}
}

// ==========================
// SERVER PASSWORD
// ==========================
window serverpassword
{
	{
		string		"_Enter server password"
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		closebutton	true
		image		ui/popup_alpha_tile
	}

	// ===================
	// PASSWORD
	// ===================
	string current_password
	{
		string		"_Password:"
		pos			"66 58"
		size		"256 20"
	}
	textentry slot_password
	{
		image		ui/button_green_verysmall
		color		"0 .78 0 1"
		selectcolor	"1 1 1 1"
		textalign	cl
		padding		8
		font		"f_verysmall"
		pos			"66 78"
		size		"256 34"
		string		*cvar:password
		ispassword	true
	}

	button set_password
	{
		string		"_OK"
		tooltip		"_Connect"
		image		ui/button_green_small
		pos			"260 170"
		width		"200"
		font		f_menu
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "mn_server_need_password 0;mn_pop;connect;" }
	}
}

// ==========================
// CHAT POPUP (IRC)
// ==========================
window chat_popup
{
	{
		string		"_Personal message"
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		closebutton	true
		image		ui/popup_alpha_tile
	}

	text chatscreen
	{
		dataid			TEXT_CHAT_WINDOW
		pos			"26 58"
		size		"444 100"
		lineheight	20
		rows		5
		tabwidth	150
		font		f_verysmall
	}

	button enter_lobby
	{
		string		"_Lobby"
		tooltip		"_Go into the lobby"
		image		ui/button_green_small
		pos			"260 170"
		width		"200"
		font		f_menu
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "mn_pop;mn_push irc;" }
	}
}

// ==========================
// GAME INVITATION
// ==========================
window multiplayer_invite
{
	{
		string		"_Game invitation"
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		closebutton	true
		image		ui/popup_alpha_tile
	}

	/* store popup data */
	string ip { invis true }
	string port { invis true }

	confunc	multiplayer_invite_server_info {
		*node:root.ip@string = <1>
		*node:root.port@string = <2>
	}

	button server_info
	{
		string		"_Server info"
		tooltip		"_Show server information"
		image		ui/button_green_small
		pos			"30 170"
		width		"200"
		font		f_menu
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{
			cmd "mn_pop;"
			cmd "server_info <node:root.ip@string> <node:root.port@string>;"
		}
	}

	button enter_lobby
	{
		string		"_Lobby"
		tooltip		"_Go into the lobby"
		image		ui/button_green_small
		pos			"260 170"
		width		"200"
		font		f_menu
		color		"0 0.5 0 1"
		selectcolor	"1 1 1 1"
		onClick		{ cmd "mn_pop;mn_push irc;" }
	}

	string invite_msg
	{
		string		"_You received a invitation to take part of a multiplayer game. Hit the connect button to connect to the server"
		pos			"30 50"
		longlines	wrap
		size		"440 60"
	}
}
