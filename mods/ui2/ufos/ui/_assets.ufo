// ==============================
// Reusable components for the UI
// ==============================

/**
 * Main menu background
 */
component main_bg extends image
{
	src	"ui/main_bg"
}


/**
 * Fuzzy screen effect
 */
component screen extends texture
{
	pos 		"0 0"
	// TODO: this should be always the full screen
	size		"2048 1536"
	bgcolor		"0 0 0 0"
	src			"ui/screen"
	ghost		true
}

/**
 * Title text top center
 */
component maintitle extends string
{
	string			"_UFO: ALIEN INVASION"
	size			"500 50"
	pos				"262 0"
	font			"f_title"
	color			"0.59 0.78 0.56 1"
	contentalign	LAYOUTALIGN_MIDDLE
}

/**
 * Toolbar for main menus
 * @TODO: Currently, the player can jump from battlescape to
 * 			options, then use this to jump to the main menu
 *			and launch another mission. Before implementing
 *			this idea, need to prevent this from happening.
 */
component MainMenuToolbar extends panel
{
	{
		size		"1024 20"
		pos			"0 0"
	}
/*

	string main_menu_str
	{
		pos				"10 0"
		size			"50 20"
		string			"_HOME"
		color			"1 1 1 0.5"
		contentalign	ALIGN_CC
		onMouseEnter 	{ *node:this@color = "1 1 1 0.9" }
		onMouseLeave 	{ *node:this@color = "1 1 1 0.5" }
		onClick			{ cmd "ui_push main;" }
	}
*/
}

/**
 * Main button templates. Can be resized as needed
 */
component MainMenuBtn extends button
{
	pos				"0 0"
	size 			"400 30"
	font			"f_normal_bold"
	color			"0.065 0.065 0.065 1"
	bgcolor			"0.4 0.52 0.5 1"
	align			LAYOUTALIGN_MIDDLE
	contentalign	ALIGN_CC
	onMouseEnter 	{ *node:this@bgcolor = "0.58 0.81 0.76 1" }
	onMouseLeave 	{ *node:this@bgcolor = "0.4 0.52 0.5 1" }
}

component MainMenu2Btn extends button
{
	pos				"0 0"
	size 			"400 30"
	font			"f_normal_bold"
	color			"0.065 0.065 0.065 1"
	bgcolor			"0.53 0.61 0.21 1"
	align			LAYOUTALIGN_MIDDLE
	contentalign	ALIGN_CC
	onMouseEnter 	{ *node:this@bgcolor = "0.82 0.92 0.44 1" }
	onMouseLeave 	{ *node:this@bgcolor = "0.53 0.61 0.21 1" }
}

component MainMenu3Btn extends button
{
	pos				"0 0"
	size 			"400 30"
	font			"f_normal_bold"
	color			"1 1 1 0.9"
	selectcolor		"0 0 0 0.9"
	bgcolor			"0.38 0.48 0.36 1"
	align			LAYOUTALIGN_MIDDLE
	contentalign	ALIGN_CC
	onMouseEnter 	{ *node:this@bgcolor = "0.88 0.20 0.075 1" }
	onMouseLeave 	{ *node:this@bgcolor = "0.65 0.13 0 1" }
}

component MainMenuExitBtn extends button
{
	pos				"0 0"
	size 			"400 30"
	font			"f_normal_bold"
	color			"1 0.68 0.61 1"
	selectcolor		"1 0.68 0.61 1"
	bgcolor			"0.65 0.13 0 1"
	align			LAYOUTALIGN_MIDDLE
	contentalign	ALIGN_CC
	onMouseEnter 	{ *node:this@bgcolor = "0.88 0.20 0.075 1" }
	onMouseLeave 	{ *node:this@bgcolor = "0.65 0.13 0 1" }
}

component MainMenuTab extends button
{
	size 		"125 30"
	font		"f_small_bold"
	bgcolor		"0.4 0.515 0.5 0.25"
	color		"1 1 1 0.5"
}

/**
 * Simple text button, left aligned. Light color.
 */
component TextBtn extends button {
	size			"250 20"
	color			"1 1 1 0.5"
	contentalign	ALIGN_CL
}

/**
 * Stats bar
 * @TODO: should change color (red/yellow/green) depending
 * on how good the value is.
 */
component StatBar extends panel
{
	{
		size		"180 25"
	}
	string label
	{
		pos				"-3 0"
		size			"80 15"
		font			"f_verysmall"
		color			"0.56 0.81 0.76 1"
		longlines		LONGLINES_CHOP
		contentalign	ALIGN_UL
	}
	texture bg
	{
		pos			"0 14"
		size		"180 10"
		src			"ui/bar_bg"
	}
	bar valuebar
	{
		pos			"0 14"
		size		"180 10"
		color		".1875 .65625 .164 1"
		max			100
		disabled	true
		nohover		true
	}
	string valuenum
	{
		pos				"90 0"
		size			"90 0"
		font			"f_verysmall"
		color			"0.56 0.81 0.76 1"
		longlines		LONGLINES_CHOP
		contentalign	ALIGN_UR
	}
}

component checkbox_ui2 extends checkbox
{
	iconChecked      "icons/checkbox_ui2_checked"
	iconUnchecked    "icons/checkbox_ui2_unchecked"
	iconIndeterminate "icons/checkbox_ui2_indeterminate"
	size "20 20"
}

/*
 * Shared window to display item details.
 * Used in market, soldier equipping, etc.
 */
component ObjectInfo extends panel
{
	{
		size			"715 180"
		border			"2"
		bordercolor		"0.527 0.6 0.21 1"
	}
	// Object titles
	panel header
	{
		{
			pos			"1 1"
			size		"713 32"
			bgcolor		"0.527 0.6 0.21 0.2"
		}
		button ufopedia
		{
			icon		"icons/bordered_magnifying_glass"
			tooltip		"_View UFOpaedia entry"
			pos			"10 9"
			size		"16 16"
			onClick		{ cmd "market_openpedia;" }
		}
		string title
		{
			string			*cvar:mn_itemname
			pos				"30 1"
			size			"670 32"
			contentalign	ALIGN_CL
			color			"0.56 0.81 0.76 1"
		}
	}
	// Model and weapon/firemode panel
	panel display
	{
		{
			pos			"450 33"
			size		"265 147"
		}
		model object_model
		{
			src			*cvar:mn_item
			pos			"0 0"
			size		"265 147"
			angles		"0 180 90"
			autoscale	true
		}
		string object_weapon
		{
			string	*cvar:mn_linkname
			pos		"0 114"
			size	"130 32"
			color	"0.56 0.81 0.76 0.7"
			bgcolor "0 0 0 0.5"
			font	"f_small"
			contentalign	ALIGN_CC
			onClick { cmd "mn_increaseitem;" }
		}
		string object_firemode
		{
			string	*cvar:mn_firemodename
			pos		"134 114"
			size	"130 32"
			color	"0.56 0.81 0.76 0.7"
			bgcolor "0 0 0 0.5"
			font	"f_small"
			contentalign	ALIGN_CC
			onClick { cmd "mn_increasefiremode;" }
		}
	}
	// Object info and stats
	panel info
	{
		{
			pos			"0 33"
			size		"450 147"
		}
		text description
		{
			dataid		TEXT_ITEMDESCRIPTION
			pos			"30 10"
			size		"420 145"
			rows		"8"
			lineheight	15
			tabwidth	168
			color		"0.56 0.81 0.76 1"
			font		"f_verysmall"
			onViewChange {
				*node:parent.description_scroll@fullsize = <fullsize>
				*node:parent.description_scroll@current = <viewpos>
			}
			onWheel {
				*node:parent.description_scroll@current = <viewpos>
			}
		}
		vscrollbar description_scroll
		{
			image		"ui/scrollbar_v_green"
			pos			"30 0"
			size		"420 145"
			current		0
			viewsize	8
			fullsize	8
			hidewhenunused true
			onChange {
				*node:parent.description@viewpos = <current>
			}
		}
	}
}

component spinner_green extends spinner
{
	topIcon	"ui/spinner_up"
	bottomIcon	"ui/spinner_down"
	size "15 19"
}
